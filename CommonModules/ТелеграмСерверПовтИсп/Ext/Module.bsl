Функция HTTPСоединение(Сервер, Порт = Неопределено, Пользователь = Неопределено, Пароль = Неопределено, Прокси = Неопределено, Таймаут = Неопределено) Экспорт
	
	ЗащищенноеСоединение = Новый ЗащищенноеСоединениеOpenSSL;
	HTTPСоединение = Новый HTTPСоединение(Сервер, Порт, Пользователь, 
		Пароль, Прокси, Таймаут, ЗащищенноеСоединение);
		
	Возврат HTTPСоединение;	
	
КонецФункции
	
Функция НастройкиБота(Бот) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Бот", Бот);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТелеграмБоты.Ссылка КАК Бот,
	|	ТелеграмБоты.АдресАпи,
	|	ТелеграмБоты.Токен,
	|	ТелеграмБоты.Наименование КАК Имя,
	|	ТелеграмБоты.Используется,
	|	ТелеграмБоты.ОграниченныйДоступ,
	|	ТелеграмБоты.ИспользуетсяПрокси КАК ИспользуетсяПрокси,
	|	ТелеграмБоты.НастройкаПрокси.Адрес КАК ПроксиАдрес,
	|	ТелеграмБоты.НастройкаПрокси.Порт КАК ПроксиПорт,
	|	ТелеграмБоты.НастройкаПрокси.Протокол КАК ПроксиПротокол,
	|	ТелеграмБоты.НастройкаПрокси.Пользователь КАК ПроксиПользователь,
	|	ТелеграмБоты.НастройкаПрокси.Пароль КАК ПроксиПароль,
	|	ТелеграмБоты.Отладка,
	|	ТелеграмБоты.ЧатПриОтладке.Ид КАК ИдентификаторЧатаПриОтладке,
	|	ТелеграмБоты.ВебхукКлюч,
	|	ТелеграмБоты.СпособСвязи,
	|	ТелеграмБоты.Таймаут,
	|	ТелеграмБоты.ТаймаутСоединения,
	|	ТелеграмБоты.СпециальныеОбработки.(
	|		Ссылка,
	|		НомерСтроки,
	|		Используется,
	|		Условие,
	|		Обработка
	|	)
	|ИЗ
	|	Справочник.ТелеграмБоты КАК ТелеграмБоты
	|ГДЕ
	|	ТелеграмБоты.Ссылка = &Бот";

	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	Возврат Выборка;
	
КонецФункции

Функция КартаВыполнения(Бот) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Бот", Бот);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТелеграмСервисыБоты.Ссылка,
	|	ТелеграмСервисыБоты.Ссылка.Наименование КАК Наименование,
	|	ТелеграмСервисыБоты.Ссылка.Код КАК Код,
	|	ТелеграмСервисыБоты.Ссылка.Сортировка
	|ПОМЕСТИТЬ Сервисы
	|ИЗ
	|	Справочник.ТелеграмСервисы.Боты КАК ТелеграмСервисыБоты
	|ГДЕ
	|	ТелеграмСервисыБоты.Бот = &Бот
	|	И ТелеграмСервисыБоты.Ссылка.Используется
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Сервисы.Ссылка,
	|	Сервисы.Наименование,
	|	Сервисы.Код
	|ИЗ
	|	Сервисы КАК Сервисы
	|
	|УПОРЯДОЧИТЬ ПО
	|	Сервисы.Сортировка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТелеграмСервисыУсловия.Ссылка КАК Сервис,
	|	ТелеграмСервисыУсловия.НомерСтроки,
	|	ТелеграмСервисыУсловия.Родитель,
	|	ТелеграмСервисыУсловия.Код,
	|	ТелеграмСервисыУсловия.ЛогическаяГруппа,
	|	ТелеграмСервисыУсловия.Условие,
	|	ТелеграмСервисыУсловия.ВидСравнения,
	|	ТелеграмСервисыУсловия.Значение
	|ИЗ
	|	Справочник.ТелеграмСервисы.Условия КАК ТелеграмСервисыУсловия
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Сервисы КАК Сервисы
	|		ПО ТелеграмСервисыУсловия.Ссылка = Сервисы.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Сервисы.Сортировка,
	|	Сервисы.Код,
	|	ТелеграмСервисыУсловия.НомерСтроки
	|ИТОГИ ПО
	|	Сервис
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТелеграмСервисыОбработки.Ссылка КАК Сервис,
	|	ТелеграмСервисыОбработки.НомерСтроки КАК НомерСтроки,
	|	ТелеграмСервисыОбработки.Обработка,
	|	ТелеграмСервисыОбработки.Обработка.Наименование КАК Наименование,
	|	ТелеграмСервисыОбработки.Обработка.КодОбработки КАК КодОбработки
	|ИЗ
	|	Справочник.ТелеграмСервисы.Обработки КАК ТелеграмСервисыОбработки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Сервисы КАК Сервисы
	|		ПО ТелеграмСервисыОбработки.Ссылка = Сервисы.Ссылка
	|ГДЕ
	|	ТелеграмСервисыОбработки.Обработка.Используется
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Пакет = Запрос.ВыполнитьПакет();
	Возврат Пакет;
	
КонецФункции

Функция ОписаниеМетода(Метод) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Метод", Метод);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТелеграмМетоды.Ссылка,
	|	ТелеграмМетоды.Код,
	|	ТелеграмМетоды.Наименование,
	|	ТелеграмМетоды.Метод,
	|	ТелеграмМетоды.СпособЗапроса,
	|	ТелеграмМетоды.ФункцияМетода,
	|	ТелеграмМетоды.Параметры.(
	|		Ссылка,
	|		НомерСтроки,
	|		Наименование,
	|		Параметр,
	|		ОбязателенДляЗаполнения,
	|		ОбязателенДляПередачи,
	|		Тип,
	|		Описание,
	|		ЕстьПроцедура,
	|		ДвоичныеДанные,
	|		ОтслеживатьПоКонтрольнойСумме,
	|		ПараметрОтслеживания
	|	) КАК ПараметрыМетода
	|ИЗ
	|	Справочник.ТелеграмМетоды КАК ТелеграмМетоды
	|ГДЕ
	|	ТелеграмМетоды.Метод = &Метод";
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		ОписаниеМетода = Неопределено;
	Иначе
		ОписаниеМетода = Результат.Выгрузить()[0];
	КонецЕсли;
	
	Возврат ОписаниеМетода;
	
КонецФункции

Функция ОписаниеКлавиатуры(Клавиатура) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Клавиатура", Клавиатура);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТелеграмКлавиатуры.Вид,
	|	ТелеграмКлавиатуры.Авторазмер,
	|	ТелеграмКлавиатуры.СкрыватьПослеИспользования,
	|	ТелеграмКлавиатуры.Программная,
	|	ТелеграмКлавиатуры.КодКлавиатуры
	|ИЗ
	|	Справочник.ТелеграмКлавиатуры КАК ТелеграмКлавиатуры
	|ГДЕ
	|	ТелеграмКлавиатуры.Ссылка = &Клавиатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТелеграмКлавиатурыКнопки.НомерСтроки КАК НомерСтроки,
	|	ТелеграмКлавиатурыКнопки.Строка КАК Строка,
	|	ТелеграмКлавиатурыКнопки.Текст,
	|	ТелеграмКлавиатурыКнопки.ДанныеОтправки,
	|	ТелеграмКлавиатурыКнопки.URL,
	|	ТелеграмКлавиатурыКнопки.ЗапроситьКонтакт,
	|	ТелеграмКлавиатурыКнопки.ЗапроситьМестоположение
	|ИЗ
	|	Справочник.ТелеграмКлавиатуры.Кнопки КАК ТелеграмКлавиатурыКнопки
	|ГДЕ
	|	ТелеграмКлавиатурыКнопки.Ссылка = &Клавиатура
	|
	|УПОРЯДОЧИТЬ ПО
	|	Строка,
	|	НомерСтроки
	|ИТОГИ ПО
	|	Строка";
	
	ПакетРезультатов = Запрос.ВыполнитьПакет();
	Возврат ПакетРезультатов;
	
КонецФункции

Функция СервисыДляВыполнения_ПустаяТаблицаУсловий() Экспорт
	
	ТаблицаУсловий = Новый ТаблицаЗначений;
	Для Каждого Реквизит Из Метаданные.Справочники.ТелеграмСервисы.ТабличныеЧасти.Условия.Реквизиты Цикл
		ТаблицаУсловий.Колонки.Добавить(Реквизит.Имя, Реквизит.Тип, Реквизит.Синоним);
	КонецЦикла;
	
	Возврат ТаблицаУсловий;
	
КонецФункции