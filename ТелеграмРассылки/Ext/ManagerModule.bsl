
Процедура ЗапуститьРассылку(ТелеграмРассылкаСсылка, ТекстОшибки = Неопределено) Экспорт
	
	Если НЕ ТелеграмРассылкаСсылка.Использовать ИЛИ НЕ ЗначениеЗаполнено(ТелеграмРассылкаСсылка.Обработка) Тогда
		Возврат
	КонецЕсли;
	
	ПараметрыРассылки = ПолучитьПараметрыРассылки(ТелеграмРассылкаСсылка);
	ПараметрыРассылки.Вставить("ТекстОшибки", ТекстОшибки);
	
	Для Каждого стр из ТелеграмРассылкаСсылка.ЧатыИБоты Цикл
		
		НазначениеОтправки = Новый Структура("Чат,Пользователь",стр.Чат,стр.Пользователь);
		
		ПараметрыЧата = РегистрыСведений.ТелеграмЗначенияПараметров.ЗначенияПараметров(стр.Чат);
		ПараметрыПользователя = ?(стр.Чат = стр.Пользователь, ПараметрыЧата,РегистрыСведений.ТелеграмЗначенияПараметров.ЗначенияПараметров(стр.Пользователь));
		НазначениеОтправки.Вставить("ПараметрыЧата", ПараметрыЧата);
		НазначениеОтправки.Вставить("ПараметрыПользователя", ПараметрыПользователя);
		
		ВыполненныеОбработки = Новый ТаблицаЗначений;
		ВыполненныеОбработки.Колонки.Добавить("Сервис", Новый ОписаниеТипов("СправочникСсылка.ТелеграмСервисы"));
		ВыполненныеОбработки.Колонки.Добавить("Обработка", Новый ОписаниеТипов("СправочникСсылка.ТелеграмОбработки"));
		НазначениеОтправки.Вставить("ВыполненныеОбработки", ВыполненныеОбработки);

		НазначениеОтправки.Вставить("ПараметрыРассылки", ПараметрыРассылки);
		НазначениеОтправки.Вставить("ИдЧата", стр.Чат.Ид);
		НазначениеОтправки.Вставить("ИдПользователя", стр.Пользователь.Ид);


		Справочники.ТелеграмОбработки.ВыполнитьОбработку(ТелеграмРассылкаСсылка.Обработка, стр.Бот, НазначениеОтправки);	
		
	КонецЦикла;
	
	Сообщить(СтрШаблон("Задание [%1] на телеграм рассылку выполнено!",ТелеграмРассылкаСсылка.Наименование));
		
КонецПроцедуры

Процедура ОповеститьОбОшибке(ТекстОшибки) Экспорт
	
	ЗапуститьРассылку(Справочники.ТелеграмРассылки.УведомлениеОбОшибке, ТекстОшибки);
	
КонецПроцедуры

Функция ПолучитьПараметрыРассылки(ТелеграмРассылкаСсылка)

	Параметры = Новый Структура();
	
	Для Каждого стр из ТелеграмРассылкаСсылка.Параметры Цикл
		
		Если стр.ЭтоМассив Тогда
			
			Если НЕ Параметры.Свойство(стр.ИмяПараметра) Тогда
				Параметры.Вставить(стр.ИмяПараметра, Новый Массив());	
			КонецЕсли;
			
			Параметры[стр.ИмяПараметра].Добавить(стр.ЗначениеПараметра);	
			
		Иначе
			
			Параметры.Вставить(стр.ИмяПараметра, стр.ЗначениеПараметра);	
			
		КонецЕсли;
		
	КонецЦикла;

	Возврат Параметры
	
КонецФункции
