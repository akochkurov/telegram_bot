
&НаКлиенте
Процедура URLОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОО = Новый ОписаниеОповещения("ОткрытьСсылкуЗавершение", ТелеграмКлиент);
	НачатьЗапускПриложения(ОО, Элемент.ТекстРедактирования);
	
КонецПроцедуры

&НаКлиенте
Процедура ПараметрПодбор(Команда)
	
	ОО = Новый ОписаниеОповещения("ПараметрПодборВыбор", ЭтаФорма);
	ОткрытьФорму("Справочник.ТелеграмМетоды.ФормаВыбора", , ЭтаФорма, УникальныйИдентификатор, , , ОО, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПараметрПодборВыбор(Результат, ДопПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("СправочникСсылка.ТелеграмМетоды") Тогда
		ДопПараметры = Новый Структура;
		ДопПараметры.Вставить("Метод", Результат);
		ОО = Новый ОписаниеОповещения("ВыбранПараметр", ЭтаФорма, ДопПараметры);
		СписокПараметров = СписокПараметров(Результат);
		СписокПараметров.ПоказатьВыборЭлемента(ОО, "Параметр?");
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СписокПараметров(Метод)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Метод", Метод);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТелеграмМетодыПараметры.Наименование
	|ИЗ
	|	Справочник.ТелеграмМетоды.Параметры КАК ТелеграмМетодыПараметры
	|ГДЕ
	|	ТелеграмМетодыПараметры.Ссылка = &Метод
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТелеграмМетодыПараметры.НомерСтроки";
	
	СписокЗначений = Новый СписокЗначений;
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		СписокЗначений.Добавить(Выборка.Наименование);
	КонецЦикла;
	
	Возврат СписокЗначений;
	
КонецФункции

&НаКлиенте
Процедура ВыбранПараметр(Результат, ДопПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("ЭлементСпискаЗначений") Тогда
		РеквизитыПараметра = РеквизитыПараметра(ДопПараметры.Метод, Результат.Значение);
		НоваяСтрока = Объект.Параметры.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, РеквизитыПараметра);
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция РеквизитыПараметра(Метод, Параметр)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Метод", Метод);
	Запрос.УстановитьПараметр("Параметр", Параметр);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТелеграмМетодыПараметры.Ссылка,
	|	ТелеграмМетодыПараметры.НомерСтроки,
	|	ТелеграмМетодыПараметры.Наименование,
	|	ТелеграмМетодыПараметры.Параметр,
	|	ТелеграмМетодыПараметры.ОбязателенДляЗаполнения,
	|	ТелеграмМетодыПараметры.ОбязателенДляПередачи,
	|	ТелеграмМетодыПараметры.Тип,
	|	ТелеграмМетодыПараметры.Описание,
	|	ТелеграмМетодыПараметры.ЕстьПроцедура,
	|	ТелеграмМетодыПараметры.ДвоичныеДанные,
	|	ТелеграмМетодыПараметры.ОтслеживатьПоКонтрольнойСумме,
	|	ТелеграмМетодыПараметры.ПараметрОтслеживания,
	|	ТелеграмМетодыПараметры.URL1,
	|	ТелеграмМетодыПараметры.URL2
	|ИЗ
	|	Справочник.ТелеграмМетоды.Параметры КАК ТелеграмМетодыПараметры
	|ГДЕ
	|	ТелеграмМетодыПараметры.Ссылка = &Метод
	|	И ТелеграмМетодыПараметры.Наименование = &Параметр";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	Ответ = Новый Структура("Наименование,Параметр,ОбязателенДляЗаполнения,
		|ОбязателенДляПередачи,Тип,Описание,ЕстьПроцедура,ДвоичныеДанные,
		|ОтслеживатьПоКонтрольнойСумме,ПараметрОтслеживания,URL1,URL2");
		
	ЗаполнитьЗначенияСвойств(Ответ, Выборка);
	Возврат Ответ;
	
КонецФункции

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ЭтаФорма.ТолькоПросмотр = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура Разблокировать(Команда)
	
	ЭтаФорма.ТолькоПросмотр = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ ПравоДоступа("Изменение", Метаданные.Справочники.ТелеграмМетоды) Тогда
		Элементы.ФормаРазблокировать.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры
