
Процедура ПриЗаписи(Отказ)
	
	ОбновитьПовторноИспользуемыеЗначения();
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если Не ЭтоГруппа Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТелеграмБоты.Ссылка
		|ИЗ
		|	Справочник.ТелеграмБоты КАК ТелеграмБоты
		|ГДЕ
		|	ТелеграмБоты.ДобавлятьВСервисы
		|	И НЕ ТелеграмБоты.ПометкаУдаления";
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			НоваяСтрока = Боты.Добавить();
			НоваяСтрока.Бот = Выборка.Ссылка;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если НЕ ЭтоГруппа Тогда
		
		БотыСтрокой = СтрСоединить(Боты.ВыгрузитьКолонку("Бот"), ", ");
		ОбработкиСтрокой = СтрСоединить(Обработки.ВыгрузитьКолонку("Обработка"), ", ");
		УсловияСтрокой = УсловиеСтрокой(1);
		Если ПометкаУдаления Тогда
			Используется = Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Функция УсловиеСтрокой(КодГруппы)
	
	СтрокаГруппы = Условия.Найти(КодГруппы, "Код");
	
	Если СтрокаГруппы.ЛогическаяГруппа = Перечисления.ТелеграмЛогическиеГруппы.ГруппаИ Тогда
		ЛогическоеУсловие = "И";
	ИначеЕсли СтрокаГруппы.ЛогическаяГруппа = Перечисления.ТелеграмЛогическиеГруппы.ГруппаИли Тогда
		ЛогическоеУсловие = "ИЛИ";
	Иначе
		ЛогическоеУсловие = " ";
	КонецЕсли;
	
	СтрокиГруппы = Условия.НайтиСтроки(Новый Структура("Родитель", КодГруппы));
	
	Ответ = "";
	Для Каждого СтрокаГруппы Из СтрокиГруппы Цикл
		Если ЗначениеЗаполнено(СтрокаГруппы.ЛогическаяГруппа) Тогда
			Ответ = СтрШаблон("%1 %2 (%3)", 
				Ответ, 
				ЛогическоеУсловие, 
				УсловиеСтрокой(СтрокаГруппы.Код));
		Иначе
			Ответ = СтрШаблон("%1 %2 %3 %4 %5", 
				Ответ, 
				?(ЗначениеЗаполнено(Ответ), ЛогическоеУсловие, ""), 
				СтрокаГруппы.Условие, 
				Перечисления.ТелеграмВидыСравнения.Представление(СтрокаГруппы.ВидСравнения), 
				СтрокаГруппы.Значение);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ответ;
	
КонецФункции