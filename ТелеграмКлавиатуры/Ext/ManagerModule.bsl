Функция УдалитьКлавиатуру() Экспорт
	
	Структура = Новый Структура;
	Структура.Вставить("hide_keyboard", Истина);
	Возврат Структура;
	
КонецФункции

Функция ПринудительныйОтвет() Экспорт
	
	Структура = Новый Структура;
	Структура.Вставить("force_reply", Истина);
	Возврат Структура;
	
КонецФункции

Функция СобратьКлавиатуру(Бот, Клавиатура, ВходящееОбновление) Экспорт
	
	ПакетРезультатов = ТелеграмСерверПовтИсп.ОписаниеКлавиатуры(Клавиатура);
	ДанныеКлавиатуры = ПакетРезультатов[0].Выгрузить()[0];
	
	Если ДанныеКлавиатуры.Программная Тогда
		Возврат ТелеграмСервер.СобратьПрограммнуюКлавиатуру(Бот, ДанныеКлавиатуры.КодКлавиатуры, ВходящееОбновление);
	КонецЕсли;
	
	Если ДанныеКлавиатуры.Вид = Перечисления.ТелеграмВидыКлавиатуры.Стандартная Тогда
		МассивСтрок = Новый Массив;
		ВыборкаСтроки = ПакетРезультатов[1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Строка");
		Пока ВыборкаСтроки.Следующий() Цикл
			МассивСтроки = Новый Массив;
			ВыборкаКнопки = ВыборкаСтроки.Выбрать();
			Пока ВыборкаКнопки.Следующий() Цикл
				СтруктураКнопки = Новый Структура;
				СтруктураКнопки.Вставить("text", ВыборкаКнопки.Текст);
				Если ВыборкаКнопки.ЗапроситьКонтакт Тогда
					СтруктураКнопки.Вставить("request_contact", ВыборкаКнопки.ЗапроситьКонтакт);
				ИначеЕсли ВыборкаКнопки.ЗапроситьМестоположение Тогда
					СтруктураКнопки.Вставить("request_location", ВыборкаКнопки.ЗапроситьМестоположение);
				КонецЕсли;
				МассивСтроки.Добавить(СтруктураКнопки);
			КонецЦикла;
			МассивСтрок.Добавить(МассивСтроки);
		КонецЦикла;
		Клавиатура = Новый Структура;
		Клавиатура.Вставить("keyboard", МассивСтрок);
		Если ДанныеКлавиатуры.Авторазмер Тогда
			Клавиатура.Вставить("resize_keyboard", ДанныеКлавиатуры.Авторазмер);
		КонецЕсли;
		Если ДанныеКлавиатуры.СкрыватьПослеИспользования Тогда
			Клавиатура.Вставить("one_time_keyboard", ДанныеКлавиатуры.СкрыватьПослеИспользования);
		КонецЕсли;
	ИначеЕсли ДанныеКлавиатуры.Вид = Перечисления.ТелеграмВидыКлавиатуры.Контекстная Тогда
		МассивСтрок = Новый Массив;
		ВыборкаСтроки = ПакетРезультатов[1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Строка");
		Пока ВыборкаСтроки.Следующий() Цикл
			МассивСтроки = Новый Массив;
			ВыборкаКнопки = ВыборкаСтроки.Выбрать();
			Пока ВыборкаКнопки.Следующий() Цикл
				СтруктураКнопки = Новый Структура;
				СтруктураКнопки.Вставить("text", ВыборкаКнопки.Текст);
				Если ЗначениеЗаполнено(ВыборкаКнопки.URL) Тогда
					СтруктураКнопки.Вставить("url", ВыборкаКнопки.URL);
				ИначеЕсли ЗначениеЗаполнено(ВыборкаКнопки.ДанныеОтправки) Тогда
					СтруктураКнопки.Вставить("callback_data", ВыборкаКнопки.ДанныеОтправки);
				КонецЕсли;
				МассивСтроки.Добавить(СтруктураКнопки);
			КонецЦикла;
			МассивСтрок.Добавить(МассивСтроки);
		КонецЦикла;
		Клавиатура = Новый Структура;
		Клавиатура.Вставить("inline_keyboard", МассивСтрок);
	КонецЕсли;
	
	Возврат Клавиатура;
	
КонецФункции