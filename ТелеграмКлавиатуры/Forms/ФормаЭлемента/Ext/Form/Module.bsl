&НаКлиенте
Процедура ПодставитьИмяПараметра(Команда)
	
	ВыбЗнач = Неопределено;
	Оповещение = Новый ОписаниеОповещения("ПослеВыбораПараметра", ЭтотОбъект);
	ПоказатьВводЗначения(Оповещение,
	ВыбЗнач, "Выберете параметр", Тип("ПланВидовХарактеристикСсылка.ТелеграмПараметры"));
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораПараметра(ВыбЗнач, Параметры) Экспорт
	
	Если ВыбЗнач <> Неопределено
		И ЗначениеЗаполнено(ВыбЗнач) Тогда
		
		ДобавитьФрагмент(""""+ ПредставлениеПараметраВИерархии(ВыбЗнач)+"""");
		
    КонецЕсли;
	
КонецПроцедуры
 
&НаСервереБезКонтекста
Функция ПредставлениеПараметраВИерархии(Параметр)
		
	Возврат ПланыВидовХарактеристик.ТелеграмПараметры.ПредставлениеПараметраВИерархии(Параметр);
	
КонецФункции

&НаСервереБезКонтекста
Функция КодФрагмента(Фрагмент)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Фрагмент", Фрагмент);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТелеграмФрагменты.Фрагмент
	|ИЗ
	|	Справочник.ТелеграмФрагменты КАК ТелеграмФрагменты
	|ГДЕ
	|	ТелеграмФрагменты.Ссылка = &Фрагмент";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	КодФрагмента = Выборка.Фрагмент;
	Возврат КодФрагмента;
	
КонецФункции

&НаКлиенте
Процедура ПодборФрагмента(Команда)
	
	ОО = Новый ОписаниеОповещения("ПодборФрагментаВыбор", ЭтаФорма);
	ОткрытьФорму("Справочник.ТелеграмФрагменты.ФормаВыбора", , ЭтаФорма, УникальныйИдентификатор, , , ОО, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодборФрагментаВыбор(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("СправочникСсылка.ТелеграмФрагменты") Тогда
		ДобавитьФрагмент(Результат);
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура ДобавитьФрагмент(Фрагмент)
	
	Если ТипЗнч(Фрагмент) = Тип("Строка") Тогда
		КодФрагмента = Фрагмент;
	Иначе
		КодФрагмента = КодФрагмента(Фрагмент);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.КодКлавиатуры) Тогда
		Объект.КодКлавиатуры = Объект.КодКлавиатуры + Символы.ПС + Символы.ПС;
	КонецЕсли;
	Объект.КодКлавиатуры = Объект.КодКлавиатуры + КодФрагмента;
	ЭтотОбъект.Модифицированность = Истина;
	
КонецПроцедуры


&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	МакетПодсказки = Справочники.ТелеграмКлавиатуры.ПолучитьМакет("ПодсказкаПоПараметрам").ПолучитьТекст();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	РеквизитПоляТекстовогоДокумента = "КодКлавиатуры";
	ЭлементПоляТекстовогоДокумента = "КодКлавиатуры";
	
	Если Объект.Программная Тогда
		ОбновитьКодКлавиатурыHTML();		
	КонецЕсли;
	УстановитьВидимостьДоступность();
	
	ПодсказкаПоВходящимПараметрам = ТелеграмКлиент.РаскраситьКод(МакетПодсказки);		

КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимостьДоступность()
	
	Элементы.Авторазмер.Видимость = Объект.Вид = ПредопределенноеЗначение("Перечисление.ТелеграмВидыКлавиатуры.Стандартная");
	Элементы.СкрыватьПослеИспользования.Видимость = Объект.Вид = ПредопределенноеЗначение("Перечисление.ТелеграмВидыКлавиатуры.Стандартная");
	Элементы.СтраницаОсновная.Видимость = НЕ Объект.Программная;
	Элементы.СтраницаКод.Видимость = Объект.Программная;
	Элементы.СтраницаРедактирование.Видимость = Объект.Программная;
	Элементы.КнопкиДанныеОтправки.Видимость = Объект.Вид = ПредопределенноеЗначение("Перечисление.ТелеграмВидыКлавиатуры.Контекстная");
	Элементы.КнопкиURL.Видимость = Объект.Вид = ПредопределенноеЗначение("Перечисление.ТелеграмВидыКлавиатуры.Контекстная");
	Элементы.КнопкиЗапроситьКонтакт.Видимость = Объект.Вид = ПредопределенноеЗначение("Перечисление.ТелеграмВидыКлавиатуры.Стандартная");
	Элементы.КнопкиЗапроситьМестоположение.Видимость = Объект.Вид = ПредопределенноеЗначение("Перечисление.ТелеграмВидыКлавиатуры.Стандартная");
	Элементы.ГруппаВид.Видимость = НЕ Объект.Программная;
	Элементы.КнопкиПоказатьКодКлавиатуры.Видимость = НЕ Объект.Программная;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидПриИзменении(Элемент)
	
	УстановитьВидимостьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура ПрограммнаяПриИзменении(Элемент)
	
	УстановитьВидимостьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура СтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	
	Если ТекущаяСтраница = Элементы.СтраницаКод Тогда
		ОбновитьКодКлавиатурыHTML();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьКодКлавиатурыHTML()
	
	КодКлавиатурыHTML = ТелеграмКлиент.РаскраситьКод(Объект.КодКлавиатуры);
	
КонецПроцедуры

&НаСервере
Функция КодКлавиатурыСервер()
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Кнопки", Объект.Кнопки.Выгрузить());
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Кнопки.НомерСтроки КАК НомерСтроки,
	|	Кнопки.Строка КАК Строка,
	|	Кнопки.Текст КАК Текст,
	|	Кнопки.ДанныеОтправки КАК ДанныеОтправки,
	|	Кнопки.URL КАК URL,
	|	Кнопки.ЗапроситьКонтакт КАК ЗапроситьКонтакт,
	|	Кнопки.ЗапроситьМестоположение КАК ЗапроситьМестоположение
	|ПОМЕСТИТЬ Кнопки
	|ИЗ
	|	&Кнопки КАК Кнопки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Кнопки.НомерСтроки КАК НомерСтроки,
	|	Кнопки.Строка КАК Строка,
	|	Кнопки.Текст КАК Текст,
	|	Кнопки.ДанныеОтправки КАК ДанныеОтправки,
	|	Кнопки.URL КАК URL,
	|	Кнопки.ЗапроситьКонтакт КАК ЗапроситьКонтакт,
	|	Кнопки.ЗапроситьМестоположение КАК ЗапроситьМестоположение
	|ИЗ
	|	Кнопки КАК Кнопки
	|
	|УПОРЯДОЧИТЬ ПО
	|	Строка,
	|	НомерСтроки
	|ИТОГИ ПО
	|	Строка";
	
	Пакет = Запрос.ВыполнитьПакет();
	РезультатКнопки = Пакет[1];
	ВидыКлавиатуры = Перечисления.ТелеграмВидыКлавиатуры;
	Код = "";
	
	Если Объект.Вид = ВидыКлавиатуры.Стандартная Тогда
		Код = Код + "МассивСтрок = Новый Массив;" + Символы.ПС;
		ВыборкаСтроки = РезультатКнопки.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Строка");
		Пока ВыборкаСтроки.Следующий() Цикл
			Код = Код + Символы.ПС + "МассивКнопокСтроки = Новый Массив;" + Символы.ПС + Символы.ПС;
			ВыборкаКнопки = ВыборкаСтроки.Выбрать();
			Пока ВыборкаКнопки.Следующий() Цикл
				Код = Код + "Кнопка = Новый Структура;" + Символы.ПС;
				Код = Код + "Кнопка.Вставить(""text"", """ + ВыборкаКнопки.Текст + """);" + Символы.ПС;
				Если ВыборкаКнопки.ЗапроситьКонтакт Тогда
					Код = Код + "Кнопка.Вставить(""request_contact"", Истина);" + Символы.ПС;
				КонецЕсли;
				Если ВыборкаКнопки.ЗапроситьМестоположение Тогда
					Код = Код + "Кнопка.Вставить(""request_location"", Истина);" + Символы.ПС;
				КонецЕсли;
				Код = Код + "МассивКнопокСтроки.Добавить(Кнопка);" + Символы.ПС + Символы.ПС;
			КонецЦикла;
			Код = Код + "МассивСтрок.Добавить(МассивКнопокСтроки);" + Символы.ПС;
		КонецЦикла;
		Код = Код + Символы.ПС;
		Код = Код + "Клавиатура = Новый Структура;" + Символы.ПС;
		Код = Код + "Клавиатура.Вставить(""keyboard"", МассивСтрок);" + Символы.ПС;
		Если Объект.Авторазмер Тогда
			Код = Код + "Клавиатура.Вставить(""resize_keyboard"", Истина);" + Символы.ПС;
		КонецЕсли;
		Если Объект.СкрыватьПослеИспользования Тогда
			Код = Код + "Клавиатура.Вставить(""one_time_keyboard"", Истина);" + Символы.ПС;
		КонецЕсли;
	ИначеЕсли Объект.Вид = ВидыКлавиатуры.Контекстная Тогда
		Код = Код + "МассивСтрок = Новый Массив;" + Символы.ПС + Символы.ПС;
		ВыборкаСтроки = РезультатКнопки.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Строка");
		Пока ВыборкаСтроки.Следующий() Цикл
			Код = Код + "МассивКнопокСтроки = Новый Массив;" + Символы.ПС + Символы.ПС;
			ВыборкаКнопки = ВыборкаСтроки.Выбрать();
			Пока ВыборкаКнопки.Следующий() Цикл
				Код = Код + "Кнопка = Новый Структура;" + Символы.ПС;
				Код = Код + "Кнопка.Вставить(""text"", """ + ВыборкаКнопки.Текст + """);" + Символы.ПС;
				Если ЗначениеЗаполнено(ВыборкаКнопки.URL) Тогда
					Код = Код + "Кнопка.Вставить(""url"", """ + ВыборкаКнопки.URL + """);" + Символы.ПС;
				ИначеЕсли ЗначениеЗаполнено(ВыборкаКнопки.ДанныеОтправки) Тогда
					Код = Код + "Кнопка.Вставить(""callback_data"", """ + ВыборкаКнопки.ДанныеОтправки + """);" + Символы.ПС;
				КонецЕсли;
				Код = Код + "МассивКнопокСтроки.Добавить(Кнопка);" + Символы.ПС + Символы.ПС;
			КонецЦикла;
			Код = Код + "МассивСтрок.Добавить(МассивКнопокСтроки);" + Символы.ПС;
		КонецЦикла;
		Код = Код + Символы.ПС;
		Код = Код + "Клавиатура = Новый Структура;" + Символы.ПС;
		Код = Код + "Клавиатура.Вставить(""inline_keyboard"", МассивСтрок);" + Символы.ПС;
	КонецЕсли;
	
	КодКлавиатуры = Справочники.ТелеграмКлавиатуры.ПолучитьМакет("КодКлавиатуры").ПолучитьТекст();
	
	Код = СтрШаблон(КодКлавиатуры, Код);
	
	Возврат Код;
	
КонецФункции

&НаКлиенте
Процедура ПоказатьКодКлавиатуры(Команда)
	
	КодКлавиатуры = КодКлавиатурыСервер();
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("КодКлавиатуры", КодКлавиатуры);
	ОткрытьФорму("Справочник.ТелеграмКлавиатуры.Форма.КодКлавиатуры", СтруктураПараметров, ЭтаФорма, УникальныйИдентификатор, , , , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодсказкаПоВходящимПараметрамПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	ТелеграмКлиент.ОткрытьСсылку(ДанныеСобытия, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура Форматировать(Команда)
	Объект.КодКлавиатуры = ТелеграмКлиент.ФорматироватьСтроку(Объект.КодКлавиатуры);
КонецПроцедуры

