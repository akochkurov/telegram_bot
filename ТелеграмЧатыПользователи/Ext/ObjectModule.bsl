
Процедура ПередЗаписью(Отказ)
	
	Если Автонаименование Тогда
		Если чТип = "private" Тогда
			Если НЕ ЗначениеЗаполнено(пФамилия) И НЕ ЗначениеЗаполнено(пИмя) И Не ЗначениеЗаполнено(пИнтернетИмя) Тогда
				Наименование = Строка(Код);
			ИначеЕсли ЗначениеЗаполнено(пИнтернетИмя) И НЕ ЗначениеЗаполнено(пФамилия) И НЕ ЗначениеЗаполнено(пИмя) Тогда
				Наименование = пИнтернетИмя;
			ИначеЕсли ЗначениеЗаполнено(пИнтернетИмя) Тогда
				Наименование = СокрЛП(СтрЗаменить(СтрШаблон("%1 %2 (%3)", пФамилия, пИмя, пИнтернетИмя), "  ", " "));
			Иначе
				Наименование = СокрЛП(СтрЗаменить(СтрШаблон("%1 %2", пФамилия, пИмя), "  ", " "));
			КонецЕсли;
		ИначеЕсли ЗначениеЗаполнено(чЗаголовок) Тогда
			Наименование = СтрШаблон("%1 (%2)", чЗаголовок, чТип);
		ИначеЕсли ЗначениеЗаполнено(пИнтернетИмя) Тогда
			Наименование = пИнтернетИмя;
		КонецЕсли;
	КонецЕсли;
	
	ОтключитьОбработчик_ПриЗаписи = Неопределено;
	ДополнительныеСвойства.Свойство("ОтключитьОбработчик_ПриЗаписи", ОтключитьОбработчик_ПриЗаписи);
	
	Если ЗначениеЗаполнено(Ссылка) И НЕ ОтключитьОбработчик_ПриЗаписи = Истина Тогда
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТелеграмЧатыПользователиБоты.Бот
		|ИЗ
		|	Справочник.ТелеграмЧатыПользователи.Боты КАК ТелеграмЧатыПользователиБоты
		|ГДЕ
		|	ТелеграмЧатыПользователиБоты.Ссылка = &Ссылка";
		
		БотыДо = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Бот");
		БотыПосле = Боты.ВыгрузитьКолонку("Бот");
		
		БотыДобавлены = Новый Массив;
		БотыУдалены = Новый Массив;
		
		Для Каждого Бот Из БотыПосле Цикл
			Если БотыДо.Найти(Бот) = Неопределено Тогда
				БотыДобавлены.Добавить(Бот);
			КонецЕсли;
		КонецЦикла;
		
		Для Каждого Бот Из БотыДо Цикл
			Если БотыПосле.Найти(Бот) = Неопределено Тогда
				БотыУдалены.Добавить(Бот);
			КонецЕсли;
		КонецЦикла;
		
		ДополнительныеСвойства.Вставить("БотыДобавлены", БотыДобавлены);
		ДополнительныеСвойства.Вставить("БотыУдалены", БотыУдалены);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	ОтключитьОбработчик_ПриЗаписи = Неопределено;
	ДополнительныеСвойства.Свойство("ОтключитьОбработчик_ПриЗаписи", ОтключитьОбработчик_ПриЗаписи);
	Если ОтключитьОбработчик_ПриЗаписи = Истина Тогда
		Возврат;
	КонецЕсли;
	
	БотыДобавлены = Неопределено;
	БотыУдалены = Неопределено;
	
	ДополнительныеСвойства.Свойство("БотыДобавлены", БотыДобавлены);
	ДополнительныеСвойства.Свойство("БотыУдалены", БотыУдалены); 
	
	Если ТипЗнч(БотыДобавлены) = Тип("Массив") Тогда
		Для Каждого Бот Из БотыДобавлены Цикл
			ВходящееОбновление = Новый Структура;
			ТелеграмСервер.ДополнитьСтруктуруВходящегоОбновления(ВходящееОбновление);
			ВходящееОбновление.Вставить("ИдЧата", Ид);
			ВходящееОбновление.Вставить("ИдПользователя", Ид);
			ВходящееОбновление.Вставить("Чат", Ссылка);
			ВходящееОбновление.Вставить("Пользователь", Ссылка);
			НастройкиБота = ТелеграмСерверПовтИсп.НастройкиБота(Бот);
			ПоляПоиска = Новый Структура("Условие, Используется", Перечисления.ТелеграмСпециальныеОбработки.ОткрытДоступ, Истина);
			ВыборкаСпециальныеОбработки = НастройкиБота.СпециальныеОбработки.Выбрать();
			Пока ВыборкаСпециальныеОбработки.НайтиСледующий(ПоляПоиска) Цикл
				Справочники.ТелеграмОбработки.ВыполнитьОбработку(
					ВыборкаСпециальныеОбработки.Обработка, 
					Бот, 
					ВходящееОбновление,,,СтрШаблон("Открыт доступ для %1", Наименование));
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
	Если ТипЗнч(БотыУдалены) = Тип("Массив") Тогда
		Для Каждого Бот Из БотыУдалены Цикл
			ВходящееОбновление = Новый Структура;
			ТелеграмСервер.ДополнитьСтруктуруВходящегоОбновления(ВходящееОбновление);			
			ВходящееОбновление.Вставить("ИдЧата", Ид);
			ВходящееОбновление.Вставить("ИдПользователя", Ид);
			ВходящееОбновление.Вставить("Чат", Ссылка);
			ВходящееОбновление.Вставить("Пользователь", Ссылка);
			НастройкиБота = ТелеграмСерверПовтИсп.НастройкиБота(Бот);
			ПоляПоиска = Новый Структура("Условие, Используется", Перечисления.ТелеграмСпециальныеОбработки.ЗакрытДоступ, Истина);
			ВыборкаСпециальныеОбработки = НастройкиБота.СпециальныеОбработки.Выбрать();
			Пока ВыборкаСпециальныеОбработки.НайтиСледующий(ПоляПоиска) Цикл
				Справочники.ТелеграмОбработки.ВыполнитьОбработку(
					ВыборкаСпециальныеОбработки.Обработка, 
					Бот, 
					ВходящееОбновление,,,СтрШаблон("Закрыт доступ для %1", Наименование));
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры