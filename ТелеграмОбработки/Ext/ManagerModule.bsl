Функция ВыполнитьОбработку(Обработка, Бот, ВходящееОбновление = Неопределено, КодОбработки = "", Сервис = Неопределено, Пояснение = "") Экспорт
	
	Если ТипЗнч(ВходящееОбновление) <> Тип("Структура") Тогда
		ВходящееОбновление = Новый Структура;
		ТелеграмСервер.ДополнитьСтруктуруВходящегоОбновления(ВходящееОбновление);
	КонецЕсли;
	
	ПрерватьВыполнение = Неопределено;
	ВходящееОбновление.Свойство("ПрерватьВыполнение", ПрерватьВыполнение);
	Если ПрерватьВыполнение = Истина Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(КодОбработки) Тогда
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Обработка", Обработка);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТелеграмОбработки.КодОбработки
		|ИЗ
		|	Справочник.ТелеграмОбработки КАК ТелеграмОбработки
		|ГДЕ
		|	ТелеграмОбработки.Ссылка = &Обработка";
		Выборка = Запрос.Выполнить().Выбрать();
		Выборка.Следующий();
		КодОбработки = Выборка.КодОбработки;
	КонецЕсли;
	
	Ошибка = Ложь;
	
	Попытка
		ТелеграмСервер.ВыполнитьКодОбработки(Бот, Обработка, КодОбработки, ВходящееОбновление);
	Исключение
		Ошибка = Истина;
		ОписаниеОшибки = ОписаниеОшибки();
	КонецПопытки;
	
	НоваяСтрока = ВходящееОбновление.ВыполненныеОбработки.Добавить();
	НоваяСтрока.Сервис = Сервис;
	НоваяСтрока.Обработка = Обработка;
	
	НомерОбновления = Неопределено;
	ВходящееОбновление.Свойство("НомерОбновления", НомерОбновления);
	Если ЗначениеЗаполнено(НомерОбновления) И НомерОбновления > 1 Тогда
		Если ЗначениеЗаполнено(Пояснение) Тогда
			Пояснение = Пояснение + ", ";
		КонецЕсли;
		Пояснение = Пояснение + "№ обновления " + НомерОбновления;
	КонецЕсли;
	
	Чат = Неопределено;
	ВходящееОбновление.Свойство("Чат", Чат);
	Пользователь = Неопределено;
	ВходящееОбновление.Свойство("Пользователь", Пользователь);
	
	РегистрыСведений.ТелеграмЖурнал.ДобавитьЗапись(Бот,
		Перечисления.ТелеграмСобытияЖурнала.ВыполненаОбработка,,
		Чат,
		Пользователь,,,,
		Пояснение,
		Сервис,
		Обработка,Ошибка,,ОписаниеОшибки);
		
	Если Ошибка И НЕ ВходящееОбновление.Свойство("ОписаниеОшибки") Тогда // специальная обработка (Бот.СпециальныеОбработки)
		ВходящееОбновление.Вставить("ОписаниеОшибки", ОписаниеОшибки);
		ВходящееОбновление.Вставить("ОбработкаОшибки", Обработка);
		НастройкиБота = ТелеграмСерверПовтИсп.НастройкиБота(Бот);
		ПоляПоиска = Новый Структура("Условие, Используется", Перечисления.ТелеграмСпециальныеОбработки.ОшибкаВКоде, Истина);
		ВыборкаСпециальныеОбработки = НастройкиБота.СпециальныеОбработки.Выбрать();
		Пока ВыборкаСпециальныеОбработки.НайтиСледующий(ПоляПоиска) Цикл
			Справочники.ТелеграмОбработки.ВыполнитьОбработку(
				ВыборкаСпециальныеОбработки.Обработка, 
				Бот, 
				ВходящееОбновление,,,"Ошибка в коде");
		КонецЦикла;
	КонецЕсли;
		
КонецФункции