&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СоздатьКнопкиИзбранныхМетодов();
	СоздатьКнопкиИзбранныхФрагментов();
	МакетПодсказки = Справочники.ТелеграмОбработки.ПолучитьМакет("ПодсказкаПоПараметрам").ПолучитьТекст();
	
КонецПроцедуры

&НаСервере
Процедура СоздатьКнопкиИзбранныхМетодов()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Методы.Метод.Код КАК Код,
	|	Методы.Метод.Наименование КАК Наименование,
	|	Методы.Метод.Описание КАК Описание,
	|	Методы.ИмяКнопки КАК ИмяКнопки,
	|	Методы.Сортировка КАК Сортировка
	|ИЗ
	|	РегистрСведений.ТелеграмИзбранныеМетоды КАК Методы
	|ГДЕ
	|	НЕ Методы.Метод.ПометкаУдаления
	|
	|УПОРЯДОЧИТЬ ПО
	|	Методы.Сортировка";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ИмяКоманды = "ИзбранныйМетод" + Выборка.Код;
		НаименованиеКнопки = ?(ЗначениеЗаполнено(Выборка.ИмяКнопки), Выборка.ИмяКнопки, Выборка.Наименование);
		
		НоваяКоманда = ЭтаФорма.Команды.Добавить(ИмяКоманды);
		НоваяКоманда.Заголовок = НаименованиеКнопки;
		НоваяКоманда.Подсказка = Выборка.Наименование + Символы.ПС + Выборка.Описание;
		НоваяКоманда.Действие = "Метод";
		НоваяКоманда.ИзменяетСохраняемыеДанные = Истина;
		
		НоваяКнопка = Элементы.Добавить("ИзбранныйМетод" + Выборка.Код, Тип("КнопкаФормы"), Элементы.ИзбранныеМетоды);
		НоваяКнопка.Заголовок = НаименованиеКнопки;
		НоваяКнопка.Вид = ВидКнопкиФормы.ОбычнаяКнопка;
		НоваяКнопка.ИмяКоманды = ИмяКоманды;
		НоваяКнопка.Ширина = Элементы.ПодборМетода.Ширина;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура СоздатьКнопкиИзбранныхФрагментов()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Фрагменты.Фрагмент.Код КАК Код,
	|	Фрагменты.Фрагмент.Наименование КАК Наименование,
	|	Фрагменты.Фрагмент.Комментарий КАК Описание,
	|	Фрагменты.ИмяКнопки КАК ИмяКнопки,
	|	Фрагменты.Сортировка КАК Сортировка
	|ИЗ
	|	РегистрСведений.ТелеграмИзбранныеФрагменты КАК Фрагменты
	|ГДЕ
	|	НЕ Фрагменты.Фрагмент.ПометкаУдаления
	|
	|УПОРЯДОЧИТЬ ПО
	|	Фрагменты.Сортировка";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ИмяКоманды = "ИзбранныйФрагмент" + Выборка.Код;
		НаименованиеКнопки = ?(ЗначениеЗаполнено(Выборка.ИмяКнопки), Выборка.ИмяКнопки, Выборка.Наименование);
		
		НоваяКоманда = ЭтаФорма.Команды.Добавить(ИмяКоманды);
		НоваяКоманда.Заголовок = НаименованиеКнопки;
		НоваяКоманда.Подсказка = Выборка.Наименование + Символы.ПС + Выборка.Описание;
		НоваяКоманда.Действие = "Фрагмент";
		НоваяКоманда.ИзменяетСохраняемыеДанные = Истина;
		
		НоваяКнопка = Элементы.Добавить("ИзбранныйФрагмент" + Выборка.Код, Тип("КнопкаФормы"), Элементы.ИзбранныеФрагменты);
		НоваяКнопка.Заголовок = НаименованиеКнопки;
		НоваяКнопка.Вид = ВидКнопкиФормы.ОбычнаяКнопка;
		НоваяКнопка.ИмяКоманды = ИмяКоманды;
		НоваяКнопка.Ширина = Элементы.ПодборМетода.Ширина;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура КнопкаРедактироватьИзбранноеНажатие(Элемент)
	
	ОткрытьФорму("РегистрСведений.ТелеграмИзбранное.ФормаСписка", , ЭтаФорма, , , , , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодставитьИмяПараметра(Команда)
	
	ВыбЗнач = Неопределено;
	Оповещение = Новый ОписаниеОповещения("ПослеВыбораПараметра", ЭтотОбъект);
	ПоказатьВводЗначения(Оповещение,
	ВыбЗнач, "Выберете параметр", Тип("ПланВидовХарактеристикСсылка.ТелеграмПараметры"));
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораПараметра(ВыбЗнач, Параметры) Экспорт
	
	Если ВыбЗнач <> Неопределено
		И ЗначениеЗаполнено(ВыбЗнач) Тогда
		
		ДобавитьФрагмент(""""+ ПредставлениеПараметраВИерархии(ВыбЗнач)+"""");
		
    КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПредставлениеПараметраВИерархии(Параметр)
		
	Возврат ПланыВидовХарактеристик.ТелеграмПараметры.ПредставлениеПараметраВИерархии(Параметр);
	
КонецФункции

&НаКлиенте
Процедура Метод(Команда)
	
	Метод = МетодПоКоду(Сред(Команда.Имя, 15));
	ОткрытьФормуПодбора(Метод);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция МетодПоКоду(Код)
	
	Возврат Справочники.ТелеграмМетоды.НайтиПоКоду(Код);
	
КонецФункции

&НаКлиенте
Процедура ОткрытьФормуПодбора(Метод)
	
	СтруктураПараметров = Новый Структура("Метод", Метод);
	ОписаниеОповещения = Новый ОписаниеОповещения("ПодобранМетод", ЭтаФорма);
	ОткрытьФорму("Справочник.ТелеграмОбработки.Форма.ФормаПодбораПараметровМетода", СтруктураПараметров, ЭтаФорма, УникальныйИдентификатор, , ,ОписаниеОповещения,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодобранМетод(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(РезультатЗакрытия) = Тип("Структура") Тогда
		ДобавитьКодМетода(РезультатЗакрытия);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Избранное(Команда)
	
	ОО = Новый ОписаниеОповещения("ИзбранноеВыбор", ЭтаФорма);
	СписокЗначений = Новый СписокЗначений;
	СписокЗначений.Добавить("Методы", "Методы");
	СписокЗначений.Добавить("Фрагменты", "Фрагменты");
	СписокЗначений.ПоказатьВыборЭлемента(ОО);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзбранноеВыбор(Результат, ДП) Экспорт
	
	Если ТипЗнч(Результат) = Тип("ЭлементСпискаЗначений") Тогда
		Если Результат.Значение = "Методы" Тогда
			ОткрытьФорму("РегистрСведений.ТелеграмИзбранныеМетоды.ФормаСписка", , ЭтаФорма, , , , , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		ИначеЕсли Результат.Значение = "Фрагменты" Тогда
			ОткрытьФорму("РегистрСведений.ТелеграмИзбранныеФрагменты.ФормаСписка", , ЭтаФорма, , , , , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	
	Если ТекущаяСтраница = Элементы.СтраницаОсновная Тогда
		ОбновитьКодОбработкиHTML();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьКодОбработкиHTML()
	
	КодОбработкиHTML = ТелеграмКлиент.РаскраситьКод(Объект.КодОбработки);	
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	РеквизитПоляТекстовогоДокумента = "КодОбработки";
	ЭлементПоляТекстовогоДокумента = "КодОбработки";
	ПодсказкаПоВходящимПараметрам = ТелеграмКлиент.РаскраситьКод(МакетПодсказки);
	ОбновитьКодОбработкиHTML();
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьКодМетода(СтруктураПараметров)
	
	КодМетода = "ПараметрыМетода = Новый Структура;" + Символы.ПС;
	
	Для Каждого ОписаниеПараметра Из СтруктураПараметров.МассивПараметров Цикл
		Если СтруктураПараметров.СопроводитьКомментариями = 2 и ЗначениеЗаполнено(ОписаниеПараметра.Описание) Тогда
			Комментарий = "// " + СтрЗаменить(ОписаниеПараметра.Описание, Символы.ПС, Символы.ПС + "// ");
		Иначе
			Комментарий = Неопределено;
		КонецЕсли;
		Если ЗначениеЗаполнено(Комментарий) Тогда
			ОриентировочнаяДлинаСтроки = 90;
			Каретка = ОриентировочнаяДлинаСтроки + 3;
			Если Каретка > СтрДлина(Комментарий) Тогда
				ПозицияПробела = 0;
			Иначе
				ПозицияПробела = СтрНайти(Комментарий, " ", , Каретка);
			КонецЕсли;
			Пока ПозицияПробела <> 0 Цикл
				Комментарий = Лев(Комментарий, ПозицияПробела - 1) + Символы.ПС + "// " + Сред(Комментарий, ПозицияПробела + 1);
				Каретка = Каретка + ОриентировочнаяДлинаСтроки;
				Если Каретка > СтрДлина(Комментарий) Тогда
					ПозицияПробела = 0;
				Иначе
					ПозицияПробела = СтрНайти(Комментарий, " ", , Каретка);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		КодМетода = КодМетода + ?(ЗначениеЗаполнено(Комментарий), Комментарий + Символы.ПС, "") + 
		СтрШаблон("ПараметрыМетода.Вставить(""%1"", <%2>);", ОписаниеПараметра.Параметр, ОписаниеПараметра.Тип) + Символы.ПС;
	КонецЦикла;
	
	Если СтруктураПараметров.СопроводитьОтветомСервера = 2 Тогда
		КодМетода = КодМетода + "ОтветСервера = ";
	КонецЕсли;
	
	КодМетода = КодМетода + СтруктураПараметров.ФункцияМетода;
	
	Если ЗначениеЗаполнено(Объект.КодОбработки) Тогда
		Объект.КодОбработки = Объект.КодОбработки + Символы.ПС + Символы.ПС;
	КонецЕсли;
	
	Объект.КодОбработки = Объект.КодОбработки + КодМетода;
	ЭтотОбъект.Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодборМетода(Команда)
	
	ОО = Новый ОписаниеОповещения("ПодборМетодаВыбор", ЭтаФорма);
	ОткрытьФорму("Справочник.ТелеграмМетоды.ФормаВыбора", , ЭтаФорма, УникальныйИдентификатор, , , ОО, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодборМетодаВыбор(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("СправочникСсылка.ТелеграмМетоды") Тогда
		Метод = Результат;
		СтруктураПараметров = Новый Структура("Метод", Метод);
		ОписаниеОповещения = Новый ОписаниеОповещения("ПодобранМетод", ЭтаФорма);
		ОткрытьФорму("Справочник.ТелеграмОбработки.Форма.ФормаПодбораПараметровМетода", СтруктураПараметров, ЭтаФорма, УникальныйИдентификатор, , ,ОписаниеОповещения,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Фрагмент(Команда)
	
	Фрагмент = ФрагментПоКоду(Сред(Команда.Имя, 18));
	ДобавитьФрагмент(Фрагмент);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ФрагментПоКоду(КодФрагмента)
	
	Возврат Справочники.ТелеграмФрагменты.НайтиПоКоду(КодФрагмента);
	
КонецФункции

&НаКлиенте
Процедура ПодборФрагмента(Команда)
	
	ОО = Новый ОписаниеОповещения("ПодборФрагментаВыбор", ЭтаФорма);
	ОткрытьФорму("Справочник.ТелеграмФрагменты.ФормаВыбора", , ЭтаФорма, УникальныйИдентификатор, , , ОО, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодборФрагментаВыбор(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("СправочникСсылка.ТелеграмФрагменты") Тогда
		ДобавитьФрагмент(Результат);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьФрагмент(Фрагмент)
	
	Если ТипЗнч(Фрагмент) = Тип("Строка") Тогда
		КодФрагмента = Фрагмент;
	Иначе
		КодФрагмента = КодФрагмента(Фрагмент);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.КодОбработки) Тогда
		Объект.КодОбработки = Объект.КодОбработки + Символы.ПС + Символы.ПС;
	КонецЕсли;
	Объект.КодОбработки = Объект.КодОбработки + КодФрагмента;
	ЭтотОбъект.Модифицированность = Истина;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция КодФрагмента(Фрагмент)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Фрагмент", Фрагмент);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТелеграмФрагменты.Фрагмент
	|ИЗ
	|	Справочник.ТелеграмФрагменты КАК ТелеграмФрагменты
	|ГДЕ
	|	ТелеграмФрагменты.Ссылка = &Фрагмент";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	КодФрагмента = Выборка.Фрагмент;
	Возврат КодФрагмента;
	
КонецФункции

&НаКлиенте
Процедура ПодсказкаПоВходящимПараметрамПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	ТелеграмКлиент.ОткрытьСсылку(ДанныеСобытия, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьОбработку(Команда)
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) ИЛИ Модифицированность Тогда
		ПоказатьПредупреждение(, "Необходимо сначала записать элемент");
		Возврат;
	КонецЕсли;
	
	ВыполнитьОбработкуСервер();
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьОбработкуСервер()
	
	Справочники.ТелеграмОбработки.ВыполнитьОбработку(Объект.Ссылка, Объект.Бот, , , , "Ручное выполнение");
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если НЕ ЗначениеЗаполнено(Объект.Наименование) Тогда
		Объект.Наименование = АвтонаименованиеОбработки();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция АвтонаименованиеОбработки()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Наименование", "Обработка %");
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТелеграмОбработки.Наименование
	|ИЗ
	|	Справочник.ТелеграмОбработки КАК ТелеграмОбработки
	|ГДЕ
	|	ТелеграмОбработки.Наименование ПОДОБНО &Наименование";
	
	МаксимальноеЧисло = 0;
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Попытка
			ТекущееЧисло = Число(Сред(Выборка.Наименование, 11));
		Исключение
			ТекущееЧисло = 0;
		КонецПопытки;
		Если ТекущееЧисло > МаксимальноеЧисло Тогда
			МаксимальноеЧисло = ТекущееЧисло;
		КонецЕсли;
	КонецЦикла;
	ИскомоеЧисло = МаксимальноеЧисло + 1;
	Возврат СтрШаблон("Обработка %1", Формат(ИскомоеЧисло, "ЧГ="));
	
Конецфункции

&НаКлиенте
Процедура Форматировать(Команда)
	Объект.КодОбработки = ТелеграмКлиент.ФорматироватьСтроку(Объект.КодОбработки);
КонецПроцедуры
