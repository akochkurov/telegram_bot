&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Метод = Параметры.Метод;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Метод);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТелеграмМетодыПараметры.Наименование КАК Параметр,
	|	ТелеграмМетодыПараметры.ОбязателенДляЗаполнения КАК ОбязательныйДляЗаполнения,
	|	ТелеграмМетодыПараметры.Тип,
	|	ТелеграмМетодыПараметры.Описание,
	|	ТелеграмМетодыПараметры.ОбязателенДляЗаполнения КАК Добавить,
	|	ТелеграмМетодыПараметры.URL1,
	|	ТелеграмМетодыПараметры.URL2
	|ИЗ
	|	Справочник.ТелеграмМетоды.Параметры КАК ТелеграмМетодыПараметры
	|ГДЕ
	|	ТелеграмМетодыПараметры.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТелеграмМетодыПараметры.НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТелеграмМетоды.ФункцияМетода КАК ФункцияМетода
	|ИЗ
	|	Справочник.ТелеграмМетоды КАК ТелеграмМетоды
	|ГДЕ
	|	ТелеграмМетоды.Ссылка = &Ссылка";
	
	ПакетРезультатов = Запрос.ВыполнитьПакет();
	
	ТаблицаПараметров.Загрузить(ПакетРезультатов[0].Выгрузить());
	ФункцияМетода = ПакетРезультатов[1].Выгрузить()[0].ФункцияМетода;
	
	ОписаниеHTML = Справочники.ТелеграмОбработки.ПолучитьМакет("ОписаниеHTML").ПолучитьТекст();
	
	Для Каждого ТекущаяСтрока Из ТаблицаПараметров Цикл
		ТекущаяСтрока.ОписаниеHTML = СтрШаблон(ОписаниеHTML, ТелеграмСервер.СоздатьСсылки(ТекущаяСтрока.Описание));
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если СопроводитьКомментариями = 0 Тогда
		СопроводитьКомментариями = 2;
	КонецЕсли;
	
	Если СопроводитьОтветомСервера = 0 Тогда
		СопроводитьОтветомСервера = 1;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Перенести(Команда)
	
	МассивПараметров = Новый Массив;
	Для Каждого ТекущаяСтрока Из ТаблицаПараметров Цикл
		Если ТекущаяСтрока.Добавить Тогда
			Структура = Новый Структура("Параметр, Тип, Описание",
				ТекущаяСтрока.Параметр, 
				ТекущаяСтрока.Тип, 
				ТекущаяСтрока.Описание);
			МассивПараметров.Добавить(Структура);	
		КонецЕсли;
	КонецЦикла;
	
	СтруктураОтвета = Новый Структура;
	СтруктураОтвета.Вставить("Метод", Метод);
	СтруктураОтвета.Вставить("МассивПараметров", МассивПараметров);
	СтруктураОтвета.Вставить("СопроводитьКомментариями", СопроводитьКомментариями);
	СтруктураОтвета.Вставить("СопроводитьОтветомСервера", СопроводитьОтветомСервера);
	СтруктураОтвета.Вставить("ФункцияМетода", ФункцияМетода);
	ЭтаФорма.Закрыть(СтруктураОтвета);
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаПараметровОписаниеHTMLПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	ТелеграмКлиент.ОткрытьСсылку(ДанныеСобытия, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаПараметровURLОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОО = Новый ОписаниеОповещения("ОткрытьСсылкуЗавершение", ТелеграмКлиент);
	НачатьЗапускПриложения(ОО, Элемент.ТекстРедактирования);
	
КонецПроцедуры