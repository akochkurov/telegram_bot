&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Состояние = "Остановлен";
	ИзменитьВидимостьДоступность();
	// открытие более одного окна
	ЭтаФорма.КлючУникальности = Новый УникальныйИдентификатор;
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьВидимостьДоступность()
	
	Элементы.ФормаСтарт.Пометка = ОбменЗапущен;
	Элементы.Состояние.ЦветТекста = ?(ОбменЗапущен, WebЦвета.Зеленый, WebЦвета.Красный);
	Элементы.Бот.ТолькоПросмотр = ОбменЗапущен;
	Элементы.Режим.ТолькоПросмотр = ОбменЗапущен;
	
КонецПроцедуры

&НаКлиенте
Процедура Итератор()
	
	Итератор = Итератор + 1;
	Если Итератор > 5 Тогда
		Итератор = 0;
	КонецЕсли;
	
	Состояние = "Выполняется";
	Для ИИ = 1 По Итератор Цикл
		Состояние = Состояние + " .";
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура Стоп(Команда)
	
	ОбменЗапущен = Ложь;
	Состояние = "Остановлен";
	ОтключитьОбработчикОжидания("ВыполнитьОбменЧерезФоновоеЗадание");
	ОтключитьОбработчикОжидания("ВыполнитьОбменИнтерактивно");
	ОтключитьОбработчикОжидания("Итератор");
	ИзменитьВидимостьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура Старт(Команда)
	
	Если НЕ ЗначениеЗаполнено(Объект.Бот) Тогда
		ПоказатьПредупреждение(, "Не указан бот");
		Возврат;
	КонецЕсли;
	
	ОбменЗапущен = Истина;
	Состояние = "Выполняется";
	ИзменитьВидимостьДоступность();
	НомерППЖурнала = МаксимальныйНомерПП(Объект.Бот);
	
	Если Объект.Режим = 0 Тогда
		СтартИнтерактивно();
	Иначе
		СтартФон();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СтартИнтерактивно()
	
	Итератор = 0;
	ПодключитьОбработчикОжидания("ВыполнитьОбменИнтерактивно", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьОбменИнтерактивно()
	
	ВыполнитьОбменИнтерактивноСервер(Объект.Бот);
	Если Объект.ВыводитьСообщения Тогда
		ОтразитьСобытияЖурнала();
	КонецЕсли;
	Итератор();
	ПодключитьОбработчикОжидания("ВыполнитьОбменИнтерактивно", 3.5, Истина);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ВыполнитьОбменИнтерактивноСервер(Бот)
	
	ТелеграмСервер.ПолучитьОбновления(Бот, Перечисления.ТелеграмСпособыСвязи.ОбработчикОжидания, 0);
	
КонецПроцедуры

&НаКлиенте
Процедура СтартФон()
	
	КлючЗадания = КлючЗадания(Объект.Бот);
	ИдентификаторФоновогоЗадания = НайтиАктивноеФоновоеЗадание(КлючЗадания);
	ПодключитьОбработчикОжидания("ВыполнитьОбменЧерезФоновоеЗадание", 0.1, Истина);
	
	Итератор = 0;
	ПодключитьОбработчикОжидания("Итератор", 1, Ложь);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция МаксимальныйНомерПП(Бот)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Бот", Бот);
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ТелеграмЖурнал.НомерПП КАК НомерПП
	|ИЗ
	|	РегистрСведений.ТелеграмЖурнал КАК ТелеграмЖурнал
	|ГДЕ
	|	ТелеграмЖурнал.Бот = &Бот
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерПП УБЫВ";
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат 0;
	Иначе
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		Возврат Выборка.НомерПП;
	КонецЕсли;
	
КонецФункции

&НаСервереБезКонтекста
Функция КлючЗадания(Бот)
	
	Возврат "Бот_" + Строка(Бот.УникальныйИдентификатор());
	
КонецФункции

&НаСервереБезКонтекста
Функция НайтиАктивноеФоновоеЗадание(КлючЗадания)
	
	ПоляПоиска = Новый Структура("Состояние, Ключ",
		СостояниеФоновогоЗадания.Активно,
		КлючЗадания);
		
	МассивЗаданий = ФоновыеЗадания.ПолучитьФоновыеЗадания(ПоляПоиска);
	Если МассивЗаданий.Количество() > 0 Тогда
		Возврат МассивЗаданий[0].УникальныйИдентификатор;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ВыполнитьОбменЧерезФоновоеЗадание()
	
	ИдентификаторФоновогоЗадания = ВыполнитьОбменЧерезФоновоеЗаданиеСервер(
		Объект.Бот, 
		ИдентификаторФоновогоЗадания);
		
	Если Объект.ВыводитьСообщения Тогда
		ОтразитьСобытияЖурнала();
	КонецЕсли;
		
	ПодключитьОбработчикОжидания("ВыполнитьОбменЧерезФоновоеЗадание", 2);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ВыполнитьОбменЧерезФоновоеЗаданиеСервер(Бот, ИдентификаторФоновогоЗадания)

	Если ЗначениеЗаполнено(ИдентификаторФоновогоЗадания) Тогда
		ФоновоеЗадание = ФоновыеЗадания.НайтиПоУникальномуИдентификатору(ИдентификаторФоновогоЗадания);
	Иначе
		ФоновоеЗадание = Неопределено;
	КонецЕсли;
	
	Если ФоновоеЗадание = Неопределено
		ИЛИ ФоновоеЗадание.Состояние <> СостояниеФоновогоЗадания.Активно Тогда
		ЗапуститьФоновоеЗадание = Истина;
	Иначе
		ЗапуститьФоновоеЗадание = Ложь;
	КонецЕсли;
	
	Если ЗапуститьФоновоеЗадание Тогда
		МассивПараметров = Новый Массив;
		МассивПараметров.Добавить(Бот);
		МассивПараметров.Добавить(Перечисления.ТелеграмСпособыСвязи.ОбработчикОжидания);
		Наименование = СтрШаблон("Телеграм — обновление %1", Строка(Бот));
		ФоновоеЗадание = ФоновыеЗадания.Выполнить("ТелеграмСервер.ПолучитьОбновления", МассивПараметров, , Наименование);
	КонецЕсли;
	
	Если ФоновоеЗадание <> Неопределено Тогда
		Возврат ФоновоеЗадание.УникальныйИдентификатор;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура ОтразитьСобытияЖурнала()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Бот", Объект.Бот);
	Запрос.УстановитьПараметр("НомерПП", НомерППЖурнала);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТелеграмЖурнал.НомерПП КАК НомерПП,
	|	ТелеграмЖурнал.Событие,
	|	ТелеграмЖурнал.Метод,
	|	ТелеграмЖурнал.ОтветСервера,
	|	ТелеграмЖурнал.Пусто,
	|	ТелеграмЖурнал.Пояснение,
	|	ТелеграмЖурнал.Сервис,
	|	ТелеграмЖурнал.Обработка,
	|	ТелеграмЖурнал.Ошибка,
	|	ТелеграмЖурнал.ОшибкаТ
	|ИЗ
	|	РегистрСведений.ТелеграмЖурнал КАК ТелеграмЖурнал
	|ГДЕ
	|	ТелеграмЖурнал.Бот = &Бот
	|	И ТелеграмЖурнал.НомерПП > &НомерПП
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерПП";
	
	ПроизведенОбмен = Ложь;
	ВыполненоОбработок = 0;
	ОбнаруженоОшибок1С = 0;
	ОбнаруженоОшибокТелеграм = 0;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если Выборка.Метод = "getUpdates" И НЕ Выборка.Пусто Тогда
			ПроизведенОбмен = Истина;
		КонецЕсли;
		Если Выборка.Событие = Перечисления.ТелеграмСобытияЖурнала.ВыполненаОбработка Тогда
			ВыполненоОбработок = ВыполненоОбработок + 1;
		КонецЕсли;
		Если Выборка.Ошибка Тогда
			ОбнаруженоОшибок1С = ОбнаруженоОшибок1С + 1;
		КонецЕсли;
		Если Выборка.ОшибкаТ Тогда
			ОбнаруженоОшибокТелеграм = ОбнаруженоОшибокТелеграм + 1;
		КонецЕсли;
		НомерППЖурнала = Выборка.НомерПП;
	КонецЦикла;
	
	СтрокаСообщения = "";
	Если ПроизведенОбмен Тогда
		СтрокаСообщения = "выполнен обмен";
	КонецЕсли;
	Если ВыполненоОбработок > 0 Тогда
		Если ЗначениеЗаполнено(СтрокаСообщения) Тогда
			СтрокаСообщения = СтрокаСообщения + ", ";
		КонецЕсли;
		СтрокаСообщения = СтрокаСообщения + "выполнено обработок - " + ВыполненоОбработок;
	КонецЕсли;
	Если ОбнаруженоОшибок1С + ОбнаруженоОшибокТелеграм > 0 Тогда
		Если ЗначениеЗаполнено(СтрокаСообщения) Тогда
			СтрокаСообщения = СтрокаСообщения + ", ";
		КонецЕсли;
		СтрокаСообщения = СтрокаСообщения + 
			СтрШаблон("обнаружено ошибок 1С - %1, обнаружено ошибок Телеграм - %2, подробности в журнале", 
			ОбнаруженоОшибок1С, 
			ОбнаруженоОшибокТелеграм);
	КонецЕсли;
	Если ЗначениеЗаполнено(СтрокаСообщения) Тогда
		СтрокаСообщения = Строка(ТекущаяДата()) + " — " + СтрокаСообщения;
		Сообщить(СтрокаСообщения);	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если Объект.БлокироватьЗакрытиеФормы Тогда
		СтандартнаяОбработка = Ложь;
		Отказ = Истина;
		Сообщить("Нельзя закрыть форму, пока установлен флажок 'Блокировать закрытие формы'");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыводитьСообщенияПриИзменении(Элемент)
	
	НомерППЖурнала = МаксимальныйНомерПП(Объект.Бот);
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Параметры.Бот) Тогда
		Объект.Бот = Параметры.Бот;
	КонецЕсли;
	
КонецПроцедуры