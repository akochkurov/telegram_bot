&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ТекстЗапроса = ПараметрыВыполненияКоманды.
		Источник.Элементы[ПараметрыВыполненияКоманды.
		Источник.ЭлементПоляТекстовогоДокумента].ВыделенныйТекст;
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "|", "");	
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ПараметрыВыполненияКоманды", ПараметрыВыполненияКоманды);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("КонструкторЗапроса", ТелеграмКлиент, СтруктураПараметров);
	
	Если ЗначениеЗаполнено(ТекстЗапроса) Тогда
		Попытка
			КонструкторЗапроса = Новый КонструкторЗапроса(ТекстЗапроса);
			ТекстЗапросаВалидный = Истина;
		Исключение
			ТекстЗапросаВалидный = Ложь;
		КонецПопытки;
	Иначе
		ТекстЗапросаВалидный = Истина;
	КонецЕсли;
	
	СтруктураПараметров.Вставить("ТекстЗапроса", ТекстЗапроса);
	СтруктураПараметров.Вставить("ТекстЗапросаВалидный", ТекстЗапросаВалидный);
	
	Если ЗначениеЗаполнено(ТекстЗапроса) И ТекстЗапросаВалидный Тогда
		ВыполнитьОбработкуОповещения(ОписаниеОповещения, КодВозвратаДиалога.Да);
	Иначе
		ПоказатьВопрос(ОписаниеОповещения, "Не найден текст запроса
		|(необходимо выделять весь текст запроса, без кавычек).
		|Создать новый запрос?", РежимДиалогаВопрос.ДаНет, , , "Конструктор запроса");
	КонецЕсли;
	
КонецПроцедуры