Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Условие") И ЗначениеЗаполнено(Параметры.Условие) Тогда
		
		СтандартнаяОбработка = Ложь;
		ДанныеВыбора = Новый СписокЗначений;
		ТипЗначения = Параметры.Условие.ТипЗначения;
		ВидыСравнения = Перечисления.ТелеграмВидыСравнения;
		
		Если ТипЗначения.СодержитТип(Тип("Булево")) Тогда
			Добавить(ДанныеВыбора, ВидыСравнения.Равно);
			Добавить(ДанныеВыбора, ВидыСравнения.НеРавно);
		КонецЕсли;
			
		Если ТипЗначения.СодержитТип(Тип("Строка")) Тогда
			Добавить(ДанныеВыбора, ВидыСравнения.Равно);
			Добавить(ДанныеВыбора, ВидыСравнения.НеРавно);
			Добавить(ДанныеВыбора, ВидыСравнения.Заполнено);
			Добавить(ДанныеВыбора, ВидыСравнения.НеЗаполнено);
			Добавить(ДанныеВыбора, ВидыСравнения.НачинаетсяС);
			Добавить(ДанныеВыбора, ВидыСравнения.НеНачинаетсяС);
			Добавить(ДанныеВыбора, ВидыСравнения.Содержит);
			Добавить(ДанныеВыбора, ВидыСравнения.НеСодержит);
		КонецЕсли;
		
		Если ТипЗначения.СодержитТип(Тип("Дата")) Тогда
			Добавить(ДанныеВыбора, ВидыСравнения.Равно);
			Добавить(ДанныеВыбора, ВидыСравнения.НеРавно);
			Добавить(ДанныеВыбора, ВидыСравнения.Больше);
			Добавить(ДанныеВыбора, ВидыСравнения.БольшеИлиРавно);
			Добавить(ДанныеВыбора, ВидыСравнения.Меньше);
			Добавить(ДанныеВыбора, ВидыСравнения.МеньшеИлиРавно);
			Добавить(ДанныеВыбора, ВидыСравнения.Заполнено);
			Добавить(ДанныеВыбора, ВидыСравнения.НеЗаполнено);
		КонецЕсли;
		
		Если ТипЗначения.СодержитТип(Тип("Число")) Тогда
			Добавить(ДанныеВыбора, ВидыСравнения.Равно);
			Добавить(ДанныеВыбора, ВидыСравнения.НеРавно);
			Добавить(ДанныеВыбора, ВидыСравнения.Больше);
			Добавить(ДанныеВыбора, ВидыСравнения.БольшеИлиРавно);
			Добавить(ДанныеВыбора, ВидыСравнения.Меньше);
			Добавить(ДанныеВыбора, ВидыСравнения.МеньшеИлиРавно);
			Добавить(ДанныеВыбора, ВидыСравнения.Заполнено);
			Добавить(ДанныеВыбора, ВидыСравнения.НеЗаполнено);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура Добавить(ДанныеВыбора, Значение)
	
	Если ДанныеВыбора.НайтиПоЗначению(Значение) = Неопределено Тогда
		ДанныеВыбора.Добавить(Значение);
	КонецЕсли;
	
КонецПроцедуры

Функция Представление(ВидСравнения) Экспорт
	
	ВидыСравнения = Перечисления.ТелеграмВидыСравнения;
	
	Если ВидСравнения = ВидыСравнения.Больше Тогда
		Представление = ">";
	ИначеЕсли ВидСравнения = ВидыСравнения.БольшеИлиРавно Тогда
		Представление = ">=";		
	ИначеЕсли ВидСравнения = ВидыСравнения.Меньше Тогда
		Представление = "<";		
	ИначеЕсли ВидСравнения = ВидыСравнения.МеньшеИлиРавно Тогда
		Представление = "<=";		
	ИначеЕсли ВидСравнения = ВидыСравнения.НеРавно Тогда
		Представление = "<>";		
	ИначеЕсли ВидСравнения = ВидыСравнения.Равно Тогда
		Представление = "=";		
	Иначе
		Представление = Строка(ВидСравнения);
	КонецЕсли;
	
	Возврат Представление;
	
КонецФункции