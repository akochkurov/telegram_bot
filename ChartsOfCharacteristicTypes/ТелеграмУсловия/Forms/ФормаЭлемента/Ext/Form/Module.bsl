
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Элементы.СтраницаОписание.Видимость = Ложь;
	РеквизитПоляТекстовогоДокумента = "КодОтвета";
	ЭлементПоляТекстовогоДокумента = "КодОтвета";
	Если Объект.Предопределенный Тогда
		ЭтаФорма.ТолькоПросмотр = Истина;
		Элементы.ГруппаРедактированиеНедоступно.Видимость = Истина;
		Элементы.СтраницаОсновнаяПредопределенный.Видимость = Истина;
		Элементы.СтраницаОсновная.Видимость = Ложь;
		Элементы.СтраницаРедактирование.Видимость = Ложь;
	Иначе
		Элементы.ГруппаРедактированиеНедоступно.Видимость = Ложь;
		Элементы.СтраницаОсновнаяПредопределенный.Видимость = Ложь;
	КонецЕсли;
	КодОтветаHTML = ?(Объект.Предопределенный, "", ТелеграмКлиент.РаскраситьКод(Объект.КодОтвета));
	ОписаниеHTML = Объект.Описание;
	Подсказка = ТелеграмКлиент.РаскраситьКод(Подсказка);
	
КонецПроцедуры


&НаКлиенте
Процедура СтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	
	Если ТекущаяСтраница = Элементы.СтраницаОсновная Тогда
		КодОтветаHTML = ТелеграмКлиент.РаскраситьКод(Объект.КодОтвета);
	ИначеЕсли ТекущаяСтраница = Элементы.СтраницаОсновнаяПредопределенный Тогда
		ОписаниеHTML = Объект.Описание;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверкаПараметраНаЗначение(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПроверкаПараметраНаЗначениеВыбор", ЭтаФорма);
	ОткрытьФорму("ПланВидовХарактеристик.ТелеграмУсловия.Форма.ПроверкаПараметраНаЗначение",
		,
		ЭтаФорма,
		УникальныйИдентификатор,
		,
		,
		ОписаниеОповещения,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверкаПараметраНаЗначениеВыбор(Ответ, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Ответ) = Тип("Структура") Тогда
		
		ПредставлениеИсточника = ?(Ответ.ПараметрПринадлежит = 0, "ПараметрыЧата", "ПараметрыПользователя");
		ПредставлениеПараметра = ПредставлениеПараметраВИерархии(Ответ.Параметр);
		ПредставлениеЗначения = ПредставлениеЗначения(Ответ.ЗначениеПараметра);
		
		Если ЗначениеЗаполнено(Объект.КодОтвета) Тогда
			Объект.КодОтвета = Объект.КодОтвета + Символы.ПС;
		КонецЕсли;
		
		Объект.КодОтвета = Объект.КодОтвета + 
		СтрШаблон("Ответ = ПрочитатьПараметр(Вход.%1,""%2"") = %3;",
			ПредставлениеИсточника,
			ПредставлениеПараметра,
			ПредставлениеЗначения);
			
			Если НЕ ЗначениеЗаполнено(Объект.Наименование) Тогда
				Объект.Наименование = СтрШаблон("%1.%2 = %3",
				ПредставлениеИсточника,
				ПредставлениеПараметра,
				ЗначениеСтрокой(Ответ.ЗначениеПараметра));
			КонецЕсли;
			
		Объект.ТипЗначения = Новый ОписаниеТипов("Булево");
		
	КонецЕсли;
	
КонецПроцедуры
 
&НаСервереБезКонтекста
Функция ПредставлениеПараметраВИерархии(Параметр)
		
	Возврат ПланыВидовХарактеристик.ТелеграмПараметры.ПредставлениеПараметраВИерархии(Параметр);
	
КонецФункции


&НаСервереБезКонтекста
Функция ЗначениеСтрокой(Значение)
	
	Возврат ТелеграмСервер.ЗначениеСтрокой(Значение);
	
КонецФункции

&НаКлиенте
Функция ПредставлениеЗначения(Значение)
	
	ТипЗ = ТипЗнч(Значение);
	
	Если ТипЗ = Тип("Строка") Тогда
		Ответ = """" + Значение + """";
	ИначеЕсли ТипЗ = Тип("Число") Тогда
		Ответ = Формат(Значение, "ЧГ=");
	ИначеЕсли ТипЗ = Тип("Дата") Тогда
		Ответ = "'" + Формат(Значение, "ДФ=yyyy.MM.dd:hh:mm:ss") + "'";
	ИначеЕсли ТипЗ = Тип("Булево") Тогда
		Ответ = Формат(Значение, "БЛ=Ложь; БИ=Истина");
	Иначе
		Попытка
			УИД = Значение.УникальныйИдентификатор();
			Ответ = СтрШаблон("<Ссылка с УИД %1>", УИД);
		Исключение
			Ответ = "<что-то>";
		КонецПопытки;
	КонецЕсли;
	
	Возврат Ответ;
	
КонецФункции

&НаКлиенте
Процедура ВключитьРедактирование(Команда)
	
	ЭтаФорма.ТолькоПросмотр = Ложь;
	Элементы.СтраницаОписание.Видимость = Истина;
	Элементы.ГруппаРедактированиеНедоступно.Видимость = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ОписаниеHTMLПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	ТелеграмКлиент.ОткрытьСсылку(ДанныеСобытия, СтандартнаяОбработка);
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Подсказка = ПланыВидовХарактеристик.ТелеграмУсловия.ПолучитьМакет("Подсказка").ПолучитьТекст();
	Если НЕ ПравоДоступа("Изменение", Метаданные.ПланыВидовХарактеристик.ТелеграмУсловия) Тогда
		Элементы.ФормаВключитьРедактирование.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры
