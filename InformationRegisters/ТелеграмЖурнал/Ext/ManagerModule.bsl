Процедура ДобавитьЗапись(Бот, 
			Событие, 
			СпособСвязи, 
			Чат = Неопределено,
			Пользователь = Неопределено,
			Метод = Неопределено, 
			ОтветСервера = Неопределено, 
			Пусто = Ложь, 
			Пояснение = "", 
			Сервис = Неопределено, 
			Обработка = Неопределено, 
			Ошибка1С = Ложь, 
			ОшибкаТелеграм = Ложь,
			ОписаниеОшибки = "") Экспорт
			
	НачатьТранзакцию();
	
	БлокировкаДанных = Новый БлокировкаДанных;
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.ТелеграмЖурнал");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	БлокировкаДанных.Заблокировать();
	
	Если НЕ ЗначениеЗаполнено(Бот) Тогда
		Бот = Справочники.ТелеграмБоты.ПустаяСсылка();
	КонецЕсли;
				
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Бот", Бот);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	МАКСИМУМ(ТелеграмЖурнал.НомерПП) КАК НомерПП
	|ИЗ
	|	РегистрСведений.ТелеграмЖурнал КАК ТелеграмЖурнал
	|ГДЕ
	|	ТелеграмЖурнал.Бот = &Бот";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	НомерПП = ?(Выборка.НомерПП = NULL, 1, Выборка.НомерПП + 1);
	
	МЗ = РегистрыСведений.ТелеграмЖурнал.СоздатьМенеджерЗаписи();
	МЗ.Бот = Бот;
	МЗ.НомерПП = НомерПП;
	МЗ.Дата = ТекущаяДата();
	МЗ.Событие = Событие;
	МЗ.СпособСвязи = СпособСвязи;
	МЗ.Чат = Чат;
	МЗ.Пользователь = Пользователь;
	МЗ.Метод = Метод;
	МЗ.ОтветСервера = ОтветСервера;
	МЗ.Пусто = Пусто;
	МЗ.Пояснение = Пояснение;
	МЗ.Сервис = Сервис;
	МЗ.Обработка = Обработка;
	МЗ.Ошибка = Ошибка1С;
	МЗ.ОшибкаТ = ОшибкаТелеграм;
	МЗ.ОписаниеОшибки = ОписаниеОшибки;
	МЗ.Записать();
	
	ЗафиксироватьТранзакцию();
	
	Если Ошибка1С ИЛИ ОшибкаТелеграм Тогда
		Сообщить(СтрШаблон("Зафиксирована ошибка %1 при выполнении обработки, подробности в журнале, запись №%2", ?(Ошибка1С, "1С", "Telegram"), НомерПП));
	КонецЕсли;
	
КонецПроцедуры

Процедура Очистить() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТелеграмЖурнал.Бот,
	|	ЕСТЬNULL(ТелеграмБоты.ХранитьЖурналДней, 90) КАК ХранитьЖурналДней
	|ИЗ
	|	РегистрСведений.ТелеграмЖурнал КАК ТелеграмЖурнал
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ТелеграмБоты КАК ТелеграмБоты
	|		ПО ТелеграмЖурнал.Бот = ТелеграмБоты.Ссылка";
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТелеграмЖурнал.НомерПП
	|ИЗ
	|	РегистрСведений.ТелеграмЖурнал КАК ТелеграмЖурнал
	|ГДЕ
	|	ТелеграмЖурнал.Бот = &Бот
	|	И ТелеграмЖурнал.Дата < &ДатаЖурнала";
	
	МенеджерЗаписи = РегистрыСведений.ТелеграмЖурнал.СоздатьМенеджерЗаписи();
	
	Пока Выборка.Следующий() Цикл
		ДатаЖурнала = ТекущаяДата() - Выборка.ХранитьЖурналДней * 24 * 60 * 60;
		Запрос.УстановитьПараметр("Бот", Выборка.Бот);
		Запрос.УстановитьПараметр("ДатаЖурнала", ДатаЖурнала);
		ВыборкаЗаписей = Запрос.Выполнить().Выбрать();
		Пока ВыборкаЗаписей.Следующий() Цикл
			МенеджерЗаписи.Бот = Выборка.Бот;
			МенеджерЗаписи.НомерПП = ВыборкаЗаписей.НомерПП;
			МенеджерЗаписи.Удалить();
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры