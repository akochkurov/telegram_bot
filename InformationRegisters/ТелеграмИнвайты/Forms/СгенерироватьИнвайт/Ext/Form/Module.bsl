
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Запись.Инвайт) Тогда
		ПрочитатьРазрешенияСтарогоИнвайта();
		ЭтоНовый = Ложь;
	Иначе	
		ЗаполнитьРазрешенияНовогоИнвайта();
		ЭтоНовый = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьРазрешенияСтарогоИнвайта()
	
	МассивПараметровВыдать = Неопределено;
	МассивПараметровЗабрать = Неопределено;
	ТекстИнвайта = Запись.ТекстИнвайта;
	РегистрыСведений.ТелеграмИнвайты.ЗаполнитьМассивыРазрешенийИзЗаписиИнвайта(МассивПараметровВыдать, МассивПараметровЗабрать, Запись.Инвайт);		
	
	Для каждого стр из МассивПараметровВыдать Цикл
		Строка = ТаблицаРазрешений.Добавить();
		Строка.Разрешение = стр;
		Строка.Выдать = Истина;
	КонецЦикла;
	
	Для каждого стр из МассивПараметровЗабрать Цикл
		Строка = ТаблицаРазрешений.Добавить();
		Строка.Разрешение = стр;
		Строка.Забрать = Истина;
	КонецЦикла;	
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРазрешенияНовогоИнвайта()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТелеграмПараметры.Ссылка КАК Разрешение,
	|	ТелеграмПараметры.Описание КАК Описание,
	|	ТелеграмПараметры.Представление КАК Представление
	|ИЗ
	|	ПланВидовХарактеристик.ТелеграмПараметры КАК ТелеграмПараметры
	|ГДЕ
	|	ТелеграмПараметры.Родитель = &Родитель
	|	И НЕ ТелеграмПараметры.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("Родитель", Справочники.РТК_НастройкиПрограммы.ПолучитьЗначенияВМассиве("РТК_ТелеграмГруппаПараметровРазрешенийДляИнвайтов")[0]);
	
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	
	ТаблицаРазрешений.Загрузить(РезультатЗапроса);	
		
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗаписатьНовыйИнвайт(МассивВыдать,МассивЗабрать)

	УстановитьПривилегированныйРежим(Истина);
	ТекстИнвайта = РегистрыСведений.ТелеграмИнвайты.СгенерироватьНовыйИнвайтПользователя(МассивВыдать,МассивЗабрать);
	УстановитьПривилегированныйРежим(Ложь);
	Возврат ТекстИнвайта;
	
КонецФункции

&НаКлиенте
Процедура Сгенерировать(Команда)
	
	МассивВыдать = Новый Массив();
	МассивЗабрать = Новый Массив();
	
	Для Каждого стр из ТаблицаРазрешений Цикл
		
		Если стр.Выдать Тогда
			МассивВыдать.Добавить(стр.Разрешение);
			
		ИначеЕсли стр.Забрать Тогда
			МассивЗабрать.Добавить(стр.Разрешение);
			
		КонецЕсли;
		
	КонецЦикла;
	
	ТекстИнвайта = ЗаписатьНовыйИнвайт(МассивВыдать,МассивЗабрать);
	ЭтоНовый = Ложь;
	ВладелецФормы.Элементы.Список.Обновить();

	ПрименитьОформление();
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаРазрешенийПриИзменении(Элемент)
	
	ЕстьВыдать = ТаблицаРазрешений.НайтиСтроки(Новый Структура("Выдать",Истина)).Количество()>0;
	ЕстьЗабрать = ТаблицаРазрешений.НайтиСтроки(Новый Структура("Забрать",Истина)).Количество()>0;
	
	ПараметрВыбран = Макс(ЕстьВыдать,ЕстьЗабрать);
	ПрименитьОформление();
	
КонецПроцедуры

&НаКлиенте
Процедура ПрименитьОформление()
	
	Элементы.ФормаСгенерировать.Доступность = ПараметрВыбран И ЭтоНовый;
	Элементы.ГруппаАктивацияИВыдача.Видимость = НЕ ЭтоНовый И ЗначениеЗаполнено(Запись.КтоВыдал);
	Элементы.ФормаСгенерировать.Видимость = НЕ Элементы.ГруппаАктивацияИВыдача.Видимость;
	Элементы.ТаблицаРазрешений.ТолькоПросмотр = НЕ ЭтоНовый;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ПрименитьОформление();
КонецПроцедуры

