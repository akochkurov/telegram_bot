Функция ЗначенияПараметров(ЧатПользователь) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТелеграмПараметры.Родитель КАК Группа,
	|	ТелеграмПараметры.Код КАК Параметр,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТелеграмЗначенияПараметров.Значение, НЕОПРЕДЕЛЕНО) = &ОбозначениеХранилища
	|			ТОГДА ТелеграмЗначенияПараметров.ХранилищеЗначения
	|		ИНАЧЕ ЕСТЬNULL(ТелеграмЗначенияПараметров.Значение, НЕОПРЕДЕЛЕНО)
	|	КОНЕЦ КАК Значение
	|ИЗ
	|	ПланВидовХарактеристик.ТелеграмПараметры КАК ТелеграмПараметры
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТелеграмЗначенияПараметров КАК ТелеграмЗначенияПараметров
	|		ПО ТелеграмПараметры.Ссылка = ТелеграмЗначенияПараметров.Параметр
	|			И (ТелеграмЗначенияПараметров.ЧатПользователь = &ЧатПользователь)
	|			И (НЕ ТелеграмПараметры.ЭтоГруппа)
	|ГДЕ
	|	НЕ ТелеграмПараметры.ЭтоГруппа
	|	И НЕ ТелеграмПараметры.ПометкаУдаления
	|ИТОГИ ПО
	|	Группа ИЕРАРХИЯ";
	
	Запрос.УстановитьПараметр("ЧатПользователь", ЧатПользователь);
	Запрос.УстановитьПараметр("ОбозначениеХранилища", "ЗначениеВХранилищеЗначения");

	РезультатЗапроса = Запрос.Выполнить();
	
	Дерево = РезультатЗапроса.Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
		
	ЗначенияПараметров = Новый Структура();	
	ОбработатьДеревоРекурсия(ЗначенияПараметров, Дерево);
	
	Возврат ЗначенияПараметров;
КонецФункции

Функция СоответствиеЗначенийПараметровПользователей(МассивСсылокПараметров) Экспорт
			
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТелеграмЗначенияПараметров.ЧатПользователь КАК ЧатПользователь,
		|	ТелеграмЗначенияПараметров.Параметр КАК Параметр,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(ТелеграмЗначенияПараметров.Значение, НЕОПРЕДЕЛЕНО) = &ОбозначениеХранилища
		|			ТОГДА ТелеграмЗначенияПараметров.ХранилищеЗначения
		|		ИНАЧЕ ЕСТЬNULL(ТелеграмЗначенияПараметров.Значение, НЕОПРЕДЕЛЕНО)
		|	КОНЕЦ КАК Значение
		|ИЗ
		|	РегистрСведений.ТелеграмЗначенияПараметров КАК ТелеграмЗначенияПараметров
		|ГДЕ
		|	ТелеграмЗначенияПараметров.Параметр В(&МассивСсылокПараметров)
		|ИТОГИ ПО
		|	ЧатПользователь ИЕРАРХИЯ";
	
	Запрос.УстановитьПараметр("МассивСсылокПараметров", МассивСсылокПараметров);
	Запрос.УстановитьПараметр("ОбозначениеХранилища", "ЗначениеВХранилищеЗначения");
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Дерево = РезультатЗапроса.Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	
	МассивЗначенияПараметров = Новый Массив();
	
	Для каждого стрПользователь из Дерево.Строки Цикл
		ЗначенияПараметров = Новый Соответствие;	
		
		Для каждого стрПараметр из стрПользователь.Строки Цикл
			ЗначенияПараметров.Вставить(стрПараметр.Параметр,стрПараметр.Значение);	
		КонецЦикла;	
		
		ПараметрыПользователя = Новый Структура("Пользователь,ПараметрыПользователя",стрПользователь.ЧатПользователь,ЗначенияПараметров);
		МассивЗначенияПараметров.Добавить(ПараметрыПользователя);
	КонецЦикла;	
	
	Возврат МассивЗначенияПараметров;
	
КонецФункции

Процедура ОбработатьДеревоРекурсия(Структура, Дерево)

	Для каждого стр из Дерево.Строки Цикл		
		
		Если НЕ ЗначениеЗаполнено(стр.Группа) Тогда 
			РекурсивноеЗаполнениеСтруктуры(Структура,стр.Строки);			
			
		ИначеЕсли стр.Строки.Количество()>0 Тогда
			
			ТекущаяСтрока = Новый Структура();
			РекурсивноеЗаполнениеСтруктуры(ТекущаяСтрока,стр.Строки);
			Структура.Вставить(Строка(стр.Группа),ТекущаяСтрока);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура РекурсивноеЗаполнениеСтруктуры(ТекущаяСтруктура,Строки)
	
	Для Каждого стр из Строки Цикл 
		
		Если стр.Строки.Количество()>0 Тогда
			
			Если стр.Родитель <> Неопределено И
				стр.Группа = стр.Родитель.Группа Тогда
				
				Для Каждого стрТекущаяГруппа из стр.Строки Цикл
					ТекущаяСтруктура.Вставить(Строка(стрТекущаяГруппа.Параметр), стрТекущаяГруппа.Значение);
				КонецЦикла;
				Продолжить;	
			КонецЕсли;
			
			ТекущаяСтрока = Новый Структура();
			РекурсивноеЗаполнениеСтруктуры(ТекущаяСтрока,стр.Строки);
			ТекущаяСтруктура.Вставить(Строка(стр.Группа),ТекущаяСтрока);
			
		Иначе
			Значение = стр.Значение;
			Если ТипЗнч(Значение) = Тип("ХранилищеЗначения") Тогда
				Значение = Значение.Получить();
			КонецЕсли;
			ТекущаяСтруктура.Вставить(Строка(стр.Параметр),Значение);
		КонецЕсли;
	КонецЦикла;	
	
КонецПроцедуры

Процедура ОчиститьЗначения(Бот, ЧатПользователь) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Бот", Бот);
	Запрос.УстановитьПараметр("ЧатПользователь", ЧатПользователь);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТелеграмЗначенияПараметров.ЧатПользователь,
	|	ТелеграмЗначенияПараметров.Параметр
	|ИЗ
	|	РегистрСведений.ТелеграмЗначенияПараметров КАК ТелеграмЗначенияПараметров
	|ГДЕ
	|	ТелеграмЗначенияПараметров.ЧатПользователь = &ЧатПользователь
	//|	И ТелеграмЗначенияПараметров.Параметр.Бот = &Бот
	|	И ТелеграмЗначенияПараметров.Параметр.ИсточникЗначения";
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	МЗ = РегистрыСведений.ТелеграмЗначенияПараметров.СоздатьМенеджерЗаписи();
	Пока Выборка.Следующий() Цикл
		МЗ.ЧатПользователь = Выборка.ЧатПользователь;
		МЗ.Параметр = Выборка.Параметр;
		МЗ.Удалить();
	КонецЦикла;
	
КонецПроцедуры