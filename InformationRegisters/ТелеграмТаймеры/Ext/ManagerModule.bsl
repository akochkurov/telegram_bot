Процедура УстановитьВыполнениеПоТаймеру(Бот, Обработка, ВходящееОбновление, ДатаСрабатывания, ВремяАктуальности = Неопределено, Условие = Неопределено, ЗначениеУсловия = Неопределено, Пояснение = "") Экспорт
	
	ЗаписьXML = Новый ЗаписьXML;
	ЗаписьXML.УстановитьСтроку();
	СериализаторXDTO.ЗаписатьXML(ЗаписьXML, ВходящееОбновление);
	ВходящееОбновлениеXML = ЗаписьXML.Закрыть();
	
	НачатьТранзакцию();
	
	БлокировкаДанных = Новый БлокировкаДанных;
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.ТелеграмТаймеры");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	БлокировкаДанных.Заблокировать();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	МАКСИМУМ(ТелеграмТаймеры.НомерПП) КАК НомерПП
	|ИЗ
	|	РегистрСведений.ТелеграмТаймеры КАК ТелеграмТаймеры";
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	Выборка.Следующий();
	
	НомерПП = ?(Выборка.НомерПП = NULL, 0, Выборка.НомерПП) + 1;
	
	МЗ = РегистрыСведений.ТелеграмТаймеры.СоздатьМенеджерЗаписи();
	МЗ.НомерПП = НомерПП;
	МЗ.ДатаСрабатывания = ДатаСрабатывания;
	МЗ.ВремяАктуальности = ВремяАктуальности;
	МЗ.Условие = Условие;
	МЗ.ЗначениеУсловия = ЗначениеУсловия;
	МЗ.Бот = Бот;
	МЗ.Обработка = Обработка;
	МЗ.ВходящееОбновлениеXML = ВходящееОбновлениеXML;
	МЗ.Пояснение = Пояснение;
	МЗ.Выполнено = Ложь;
	МЗ.Записать();
	
	ЗафиксироватьТранзакцию();
	
КонецПроцедуры

Процедура ВыполнитьЗапланированныеОбработки() Экспорт
	
	ДатаВремяТаймера = ТекущаяДата();
	
	МассивУдалить = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаВремяТаймера", ДатаВремяТаймера);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТелеграмТаймеры.НомерПП,
	|	ТелеграмТаймеры.ДатаСрабатывания,
	|	ТелеграмТаймеры.ВремяАктуальности,
	|	ТелеграмТаймеры.Обработка,
	|	ТелеграмТаймеры.Бот,
	|	ТелеграмТаймеры.ВходящееОбновлениеXML,
	|	ТелеграмТаймеры.Условие,
	|	ТелеграмТаймеры.ЗначениеУсловия,
	|	ТелеграмТаймеры.Пояснение
	|ИЗ
	|	РегистрСведений.ТелеграмТаймеры КАК ТелеграмТаймеры
	|ГДЕ
	|	ТелеграмТаймеры.ДатаСрабатывания <= &ДатаВремяТаймера
	|	И НЕ ТелеграмТаймеры.Выполнено";
	
	Результат = Запрос.Выполнить();
	
	ТаблицаЗначений = Результат.Выгрузить();
	ТаблицаЗначений.Колонки.Добавить("ВходящееОбновление", Новый ОписаниеТипов("Структура"));
	
	Для Каждого ТекущаяСтрока Из ТаблицаЗначений Цикл
		МЗ = РегистрыСведений.ТелеграмТаймеры.СоздатьМенеджерЗаписи();
		МЗ.НомерПП = ТекущаяСтрока.НомерПП;
		МЗ.Прочитать();
		МЗ.Выполнено = Истина;
		МЗ.ДатаВыполнения = ДатаВремяТаймера;
		МЗ.Записать();
	КонецЦикла;
	
	Для Каждого ТекущаяСтрока Из ТаблицаЗначений Цикл
		
		Если ТекущаяСтрока.ВремяАктуальности > 0 Тогда
			ГраницаВыполнения = ТекущаяСтрока.ДатаСрабатывания + ТекущаяСтрока.ВремяАктуальности;
			Если ГраницаВыполнения < ДатаВремяТаймера Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		ВходящееОбновление = Неопределено;
		
		Если ЗначениеЗаполнено(ТекущаяСтрока.Условие) Тогда
			ВходящееОбновление = ВосстановитьВходящееОбновление(ТекущаяСтрока.ВходящееОбновлениеXML);
			ЛевоеЗначение = ПланыВидовХарактеристик.ТелеграмУсловия.ЗначениеУсловия(ТекущаяСтрока.Условие, ВходящееОбновление);
			Если ЛевоеЗначение <> ТекущаяСтрока.ЗначениеУсловия Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		Если ВходящееОбновление = Неопределено Тогда
			ВходящееОбновление = ВосстановитьВходящееОбновление(ТекущаяСтрока.ВходящееОбновлениеXML);
		КонецЕсли;
		
		Пояснение = СтрШаблон("Таймер %1, %2", Формат(ТекущаяСтрока.НомерПП, "ЧГ="), ТекущаяСтрока.Пояснение);	
			
		Справочники.ТелеграмОбработки.ВыполнитьОбработку(
			ТекущаяСтрока.Обработка,
			ТекущаяСтрока.Бот,
			ВходящееОбновление,,,
			Пояснение);
		
	КонецЦикла;
	
КонецПроцедуры

Функция ВосстановитьВходящееОбновление(СтрокаXML)
	
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.УстановитьСтроку(СтрокаXML);
	ВходящееОбновление = СериализаторXDTO.ПрочитатьXML(ЧтениеXML);
	Возврат ВходящееОбновление;
	
КонецФункции

Процедура Очистить() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТелеграмТаймеры.Бот,
	|	ЕСТЬNULL(ТелеграмБоты.ХранитьЖурналДней, 0) КАК ХранитьЖурналДней
	|ИЗ
	|	РегистрСведений.ТелеграмТаймеры КАК ТелеграмТаймеры
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ТелеграмБоты КАК ТелеграмБоты
	|		ПО ТелеграмТаймеры.Бот = ТелеграмБоты.Ссылка";
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТелеграмТаймеры.НомерПП
	|ИЗ
	|	РегистрСведений.ТелеграмТаймеры КАК ТелеграмТаймеры
	|ГДЕ
	|	ТелеграмТаймеры.Выполнено
	|	И ТелеграмТаймеры.Бот = &Бот
	|	И ТелеграмТаймеры.ДатаВыполнения < &ДатаВыполнения";
	
	МенеджерЗаписи = РегистрыСведений.ТелеграмТаймеры.СоздатьМенеджерЗаписи();
	
	Пока Выборка.Следующий() Цикл
		ДатаВыполнения = ТекущаяДата() - Выборка.ХранитьЖурналДней * 24 * 60 * 60;
		Запрос.УстановитьПараметр("Бот", Выборка.Бот);
		Запрос.УстановитьПараметр("ДатаВыполнения", ДатаВыполнения);
		ВыборкаЗаписей = Запрос.Выполнить().Выбрать();
		Пока ВыборкаЗаписей.Следующий() Цикл
			МенеджерЗаписи.НомерПП = ВыборкаЗаписей.НомерПП;
			МенеджерЗаписи.Удалить();
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры