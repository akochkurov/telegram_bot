Функция СостояниеБотаВФорматеHTML(Бот) Экспорт
	
	НастройкиБота = ТелеграмСерверПовтИсп.НастройкиБота(Бот);
	
	МакетHTML = Справочники.ТелеграмБоты.ПолучитьМакет("СостояниеБота").ПолучитьТекст();
	
	Показатели = Новый Массив;
	Показатели.Добавить(ПоследнийОбмен(Бот));
	Показатели.Добавить(ПоследняяОшибка(Бот));
	Показатели.Добавить(ПоследняяОшибкаТ(Бот));
	Если НастройкиБота.СпособСвязи = Перечисления.ТелеграмСпособыСвязи.Вебхук Тогда
		Показатели.Добавить(КоличествоВебхуковЗа72часа(Бот));
	Иначе
		Показатели.Добавить(КоличествоGetUpdatesЗа72часа(Бот));
	КонецЕсли;
	Показатели.Добавить(КоличествоОшибокЗа72Часа(Бот));
	Показатели.Добавить(КоличествоОшибокЗа30Дней(Бот));
	
	ПоказателиСтр = "";
	Для Каждого Показатель Из Показатели Цикл
		ПоказателиСтр = ПоказателиСтр + Показатель;
	КонецЦикла;
	
	Ответ = СтрШаблон(МакетHTML, ПоказателиСтр);
	
	Возврат Ответ;
	
КонецФункции

Функция ПоследнийОбмен(Бот)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Бот", Бот);
	Запрос.УстановитьПараметр("Метод", "getUpdates");
	Запрос.УстановитьПараметр("Событие", Перечисления.ТелеграмСобытияЖурнала.Вебхук);
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ТелеграмЖурнал.НомерПП КАК НомерПП,
	|	ТелеграмЖурнал.Дата,
	|	ТелеграмЖурнал.Событие,
	|	ТелеграмЖурнал.СпособСвязи,
	|	ТелеграмЖурнал.Чат,
	|	ТелеграмЖурнал.Пользователь,
	|	ТелеграмЖурнал.Ошибка,
	|	ТелеграмЖурнал.ОписаниеОшибки
	|ПОМЕСТИТЬ ПоследниеОбращенияGetUpdates
	|ИЗ
	|	РегистрСведений.ТелеграмЖурнал КАК ТелеграмЖурнал
	|ГДЕ
	|	ТелеграмЖурнал.Бот = &Бот
	|	И ТелеграмЖурнал.Метод = &Метод
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерПП УБЫВ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ТелеграмЖурнал.НомерПП,
	|	ТелеграмЖурнал.Дата,
	|	ТелеграмЖурнал.Событие,
	|	ТелеграмЖурнал.СпособСвязи,
	|	ТелеграмЖурнал.Чат,
	|	ТелеграмЖурнал.Пользователь,
	|	ТелеграмЖурнал.Ошибка,
	|	ТелеграмЖурнал.ОписаниеОшибки
	|ПОМЕСТИТЬ ПоследниеОбращенияВебхук
	|ИЗ
	|	РегистрСведений.ТелеграмЖурнал КАК ТелеграмЖурнал
	|ГДЕ
	|	ТелеграмЖурнал.Бот = &Бот
	|	И ТелеграмЖурнал.Событие = &Событие
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТелеграмЖурнал.НомерПП УБЫВ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ПоследниеОбращенияGetUpdates.НомерПП КАК НомерПП,
	|	ПоследниеОбращенияGetUpdates.Дата,
	|	ПоследниеОбращенияGetUpdates.Событие,
	|	ПоследниеОбращенияGetUpdates.СпособСвязи,
	|	ПоследниеОбращенияGetUpdates.Чат,
	|	ПоследниеОбращенияGetUpdates.Пользователь,
	|	ПоследниеОбращенияGetUpdates.Ошибка,
	|	ПоследниеОбращенияGetUpdates.ОписаниеОшибки
	|ИЗ
	|	ПоследниеОбращенияGetUpdates КАК ПоследниеОбращенияGetUpdates
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПоследниеОбращенияВебхук.НомерПП,
	|	ПоследниеОбращенияВебхук.Дата,
	|	ПоследниеОбращенияВебхук.Событие,
	|	ПоследниеОбращенияВебхук.СпособСвязи,
	|	ПоследниеОбращенияВебхук.Чат,
	|	ПоследниеОбращенияВебхук.Пользователь,
	|	ПоследниеОбращенияВебхук.Ошибка,
	|	ПоследниеОбращенияВебхук.ОписаниеОшибки
	|ИЗ
	|	ПоследниеОбращенияВебхук КАК ПоследниеОбращенияВебхук
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерПП УБЫВ";
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Ответ = "<p><span class=b>Последний обмен</span> — нет данных</p>";
	Иначе
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		ПрошлоСекунд = ТекущаяДата() - Выборка.Дата;
		ПолныхМинут = Цел(ПрошлоСекунд / 60);
		ПолныхЧасов = Цел(ПрошлоСекунд / (60 * 60));
		Ответ = СтрШаблон(
		"<p>
		|	<span class=b>Последний обмен</span> — %1 (%2 секунд назад, или %3 полных минут, или %4 полных часов)
		|</p>",
		Строка(Выборка.Дата),
		Строка(ПрошлоСекунд), 
		ПолныхМинут, 
		ПолныхЧасов);
	КонецЕсли;
	
	Возврат Ответ;
	
КонецФункции

Функция ПоследняяОшибка(Бот)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Бот", Бот);
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ТелеграмЖурнал.НомерПП КАК НомерПП,
	|	ТелеграмЖурнал.Дата,
	|	ТелеграмЖурнал.Обработка.Наименование КАК ОбработкаНаименование,
	|	ТелеграмЖурнал.Обработка.Код КАК ОбработкаКод,
	|	ТелеграмЖурнал.ОписаниеОшибки
	|ИЗ
	|	РегистрСведений.ТелеграмЖурнал КАК ТелеграмЖурнал
	|ГДЕ
	|	ТелеграмЖурнал.Бот = &Бот
	|	И ТелеграмЖурнал.Ошибка
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерПП УБЫВ";
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Ответ = "<p><span class=b>Последняя ошибка 1С</span> — нет данных</p>";
	Иначе
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		ПрошлоСекунд = ТекущаяДата() - Выборка.Дата;
		ПолныхМинут = Цел(ПрошлоСекунд / 60);
		ПолныхЧасов = Цел(ПрошлоСекунд / (60 * 60));
		Ответ = СтрШаблон(
		"<p>
		|    <span class=b>Последняя ошибка 1С</span> — %1
		|    <ul>
		|        <li>Номер записи журнала — %4</li>
		|        <li>Обработка — %2 (%3)</li>
		|        <li>%5</li>
		|    </ul>
		|</p>",
		ТелеграмСервер.ЭкранированиеHTML(Строка(Выборка.Дата)),
		ТелеграмСервер.ЭкранированиеHTML(Строка(Выборка.ОбработкаНаименование)),
		ТелеграмСервер.ЭкранированиеHTML(Строка(Выборка.ОбработкаКод)),
		ТелеграмСервер.ЭкранированиеHTML(Строка(Выборка.НомерПП)),
		ТелеграмСервер.ЭкранированиеHTML(Строка(Выборка.ОписаниеОшибки)));
	КонецЕсли;
	
	Возврат Ответ;
	
КонецФункции

Функция ПоследняяОшибкаТ(Бот)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Бот", Бот);
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ТелеграмЖурнал.НомерПП КАК НомерПП,
	|	ТелеграмЖурнал.Дата КАК Дата,
	|	ТелеграмЖурнал.Метод КАК Метод,
	|	ТелеграмЖурнал.ОтветСервера КАК ОтветСервера
	|ИЗ
	|	РегистрСведений.ТелеграмЖурнал КАК ТелеграмЖурнал
	|ГДЕ
	|	ТелеграмЖурнал.Бот = &Бот
	|	И ТелеграмЖурнал.ОшибкаТ
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерПП УБЫВ";
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Ответ = "<p><span class=b>Последняя ошибка Telegram</span> — нет данных</p>";
	Иначе
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		ПрошлоСекунд = ТекущаяДата() - Выборка.Дата;
		ПолныхМинут = Цел(ПрошлоСекунд / 60);
		ПолныхЧасов = Цел(ПрошлоСекунд / (60 * 60));
		Ответ = СтрШаблон(
		"<p>
		|    <span class=b>Последняя ошибка Telegram</span> — %1
		|    <ul>
		|        <li>Номер записи журнала — %2</li>
		|        <li>Метод — %3</li>
		|        <li>Ответ сервера — %4</li>
		|    </ul>
		|</p>",
		ТелеграмСервер.ЭкранированиеHTML(Строка(Выборка.Дата)),
		ТелеграмСервер.ЭкранированиеHTML(Строка(Выборка.НомерПП)),
		ТелеграмСервер.ЭкранированиеHTML(Строка(Выборка.Метод)),
		ТелеграмСервер.ЭкранированиеHTML(Строка(Выборка.ОтветСервера)));
	КонецЕсли;
	
	Возврат Ответ;
	
КонецФункции

Функция КоличествоGetUpdatesЗа72часа(Бот)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Бот", Бот);
	Запрос.УстановитьПараметр("ДатаНачала", ТекущаяДата() - 3 * 24 * 60 * 60);
	Запрос.УстановитьПараметр("Метод", "getUpdates");
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СУММА(1) КАК Запросов,
	|	СУММА(ВЫБОР
	|			КОГДА ТелеграмЖурнал.Пусто
	|				ТОГДА 0
	|			ИНАЧЕ 1
	|		КОНЕЦ) КАК НепустыхЗапросов
	|ИЗ
	|	РегистрСведений.ТелеграмЖурнал КАК ТелеграмЖурнал
	|ГДЕ
	|	ТелеграмЖурнал.Бот = &Бот
	|	И ТелеграмЖурнал.Дата >= &ДатаНачала
	|	И ТелеграмЖурнал.Метод = &Метод";
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	Выборка.Следующий();
	
	Запросов = ?(Выборка.Запросов = NULL, 0, Выборка.Запросов);
	СекундНаЗапрос = ?(Запросов = 0, 0, 3 * 24 * 60 * 60 / Запросов);
	НепустыхЗапросов = ?(Выборка.НепустыхЗапросов = NULL, 0, Выборка.НепустыхЗапросов);
	
	Ответ = СтрШаблон(
	"<p>
	|	<span class=b>Количество getUpdates за 72 часа</span> — %1 (среднеарифм. %2 секунд между запросами), из них непустых — %3
	|</p>",
	Строка(Запросов),
	Строка(Формат(СекундНаЗапрос, "ЧДЦ=3; ЧН=н/д")),
	Строка(НепустыхЗапросов));
	
	Возврат Ответ;
	
КонецФункции

Функция КоличествоВебхуковЗа72часа(Бот)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Бот", Бот);
	Запрос.УстановитьПараметр("Событие", Перечисления.ТелеграмСобытияЖурнала.Вебхук);
	Запрос.УстановитьПараметр("ДатаНачала", ТекущаяДата() - 3 * 26 * 60 * 60);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СУММА(1) КАК Запросов
	|ИЗ
	|	РегистрСведений.ТелеграмЖурнал КАК ТелеграмЖурнал
	|ГДЕ
	|	ТелеграмЖурнал.Бот = &Бот
	|	И ТелеграмЖурнал.Событие = &Событие
	|	И ТелеграмЖурнал.Дата >= &ДатаНачала";
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	Выборка.Следующий();
	
	Запросов = ?(Выборка.Запросов = NULL, 0, Выборка.Запросов);
	Ответ = СтрШаблон(
	"<p>
	|	<span class=b>Количество вебхуков за 72 часа</span> — %1
	|</p>",
	Строка(Запросов));
	
	Возврат Ответ;
	
КонецФункции

Функция КоличествоОшибокЗа72Часа(Бот)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Бот", Бот);
	Запрос.УстановитьПараметр("ДатаНачала", ТекущаяДата() - 3 * 24 * 60 * 60);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СУММА(1) КАК Ошибок
	|ИЗ
	|	РегистрСведений.ТелеграмЖурнал КАК ТелеграмЖурнал
	|ГДЕ
	|	ТелеграмЖурнал.Бот = &Бот
	|	И ТелеграмЖурнал.Ошибка
	|	И ТелеграмЖурнал.Дата >= &ДатаНачала";
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	Выборка.Следующий();
	
	Ошибок = ?(Выборка.Ошибок = NULL, 0, Выборка.Ошибок);
	Ответ = СтрШаблон(
	"<p>
	|	<span class=b>Количество ошибок 1С за 72 часа</span> — %1
	|</p>",
	Строка(Ошибок));
	
	Возврат Ответ;
	
КонецФункции

Функция КоличествоОшибокЗа30Дней(Бот)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Бот", Бот);
	Запрос.УстановитьПараметр("ДатаНачала", ТекущаяДата() - 30 * 24 * 60 * 60);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СУММА(1) КАК Ошибок
	|ИЗ
	|	РегистрСведений.ТелеграмЖурнал КАК ТелеграмЖурнал
	|ГДЕ
	|	ТелеграмЖурнал.Бот = &Бот
	|	И ТелеграмЖурнал.Ошибка
	|	И ТелеграмЖурнал.Дата >= &ДатаНачала";
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	Выборка.Следующий();
	
	Ошибок = ?(Выборка.Ошибок = NULL, 0, Выборка.Ошибок);
	Ответ = СтрШаблон(
	"<p>
	|	<span class=b>Количество ошибок 1С за 30 дней</span> — %1
	|</p>",
	Строка(Ошибок));
	
	Возврат Ответ;
	
КонецФункции