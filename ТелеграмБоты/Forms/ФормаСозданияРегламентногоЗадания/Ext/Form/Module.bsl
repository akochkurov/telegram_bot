&НаКлиенте
Процедура СоздатьИЗакрыть(Команда)
	
	ИдентификаторРегламентногоЗадания = СоздатьРегламентноеЗадание();
	ЭтаФорма.Закрыть(ИдентификаторРегламентногоЗадания);
	
КонецПроцедуры

&НаСервере
Функция СоздатьРегламентноеЗадание()
	
	РегламентноеЗадание = РегламентныеЗадания.СоздатьРегламентноеЗадание(Метаданные.РегламентныеЗадания.ТелеграмПолучениеОбновлений);
	РегламентноеЗадание.Наименование = Наименование;
	РегламентноеЗадание.ИмяПользователя = ИмяПользователя;
	РегламентноеЗадание.Расписание = Расписание;
	РегламентноеЗадание.Использование = Использование;
	РегламентноеЗадание.Ключ = Строка(Новый УникальныйИдентификатор);
	РегламентноеЗадание.Параметры.Добавить(Бот);
	РегламентноеЗадание.Параметры.Добавить(Перечисления.ТелеграмСпособыСвязи.РегламентноеЗадание);
	РегламентноеЗадание.Записать();
	
	Возврат РегламентноеЗадание.УникальныйИдентификатор;
	
КонецФункции

&НаКлиенте
Процедура ИмяПользователяНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	МассивПользователей = ПолучитьПользователей();
	СписокВыбора = Новый СписокЗначений;
	СписокВыбора.ЗагрузитьЗначения(МассивПользователей);
	СписокВыбора.ПоказатьВыборЭлемента(
		Новый ОписаниеОповещения("ИмяПользователяВыбор", ЭтаФорма), 
		"Выберите пользователя", ИмяПользователя);
	
КонецПроцедуры
	
&НаКлиенте
Процедура ИмяПользователяВыбор(ЭлементСпискаЗначений, ДП) Экспорт
	
	Если ТипЗнч(ЭлементСпискаЗначений) = Тип("ЭлементСпискаЗначений") Тогда
		ИмяПользователя = ЭлементСпискаЗначений.Значение;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьПользователей()
	
	МассивПользователей = Новый Массив;
	ПользователиИБ = ПользователиИнформационнойБазы.ПолучитьПользователей();
	Для Каждого ПользовательИБ Из ПользователиИБ Цикл
		МассивПользователей.Добавить(ПользовательИБ.Имя);
	КонецЦикла;
	Возврат МассивПользователей;
	
КонецФункции

&НаКлиенте
Процедура РасписаниеСтрокойНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если Расписание = Неопределено Тогда
		Расписание = Новый РасписаниеРегламентногоЗадания;
	КонецЕсли;
	
	Диалог = Новый ДиалогРасписанияРегламентногоЗадания(Расписание);
	Диалог.Показать(Новый ОписаниеОповещения("РасписаниеВыбор", ЭтаФорма));
	
КонецПроцедуры

&НаКлиенте
Процедура РасписаниеВыбор(Результат, ДП) Экспорт
	
	Если ТипЗнч(Результат) = Тип("РасписаниеРегламентногоЗадания") Тогда
		Расписание = Результат;
		РасписаниеСтрокой = Строка(Расписание);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Бот = Параметры.Бот;
	Наименование = СтрШаблон("Телеграм — обновления %1", Строка(Бот));
	
КонецПроцедуры